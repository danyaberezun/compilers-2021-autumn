-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

var void = 16445630;

fun eval (c@[s, w], expr) {
  case expr of
    Var (ident) -> [c, s(ident)]
    | Const (i) -> [c, i]
    | Ref (ident) -> [c, ident]
    | Ignore (exp) -> eval(exp)
    | Binop (op, lexp, rexp) ->
      case evalList(c, {lexp, rexp}) of
        [c, l:r:_] -> [c, evalOp(op, l, r)]
      esac
    | Assn (lexp, rexp) ->
      case evalList(c, {lexp, rexp}) of
        [c, l:r:_] -> [[s <- [l, r], w], r]
      esac
    | Seq (lexp, rexp) ->
      case eval(c, lexp) of
        [c, _] -> eval(c, rexp)
      esac
    | Read (ident) -> var nw = readWorld(w); [[s <- [ident, nw[0]], nw[1]], void]
    | Write (exp) ->
      case eval(c, exp) of
        [[s, w], v] -> [[s, writeWorld(v, w)], void]
      esac
    | If (exp, ifTrue, ifFalse) ->
      case eval(c, exp) of
        [c, v] -> if v != 0 then eval(c, ifTrue) else eval(c, ifFalse) fi
      esac
    | While (exp, s) ->
      case eval(c, exp) of
        [c, v] -> if v != 0 then 
          case eval(c, s) of
            [c, _] -> eval(c, expr)
          esac
        else [c, void] fi
      esac
    | DoWhile (s, exp) ->
      case eval(c, s) of
        [c, _] ->
          case eval(c, exp) of
            [c, v] -> if v != 0 then eval(c, expr) else [c, void] fi
          esac
      esac
    | Skip -> [c, void]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
