-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (defs, expr)         |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun addDefs (st, defs) {
  foldl (fun (st, d) {
    case d of
      Fun (name, args, body) -> st.addFunction (name, args, body)
    | Var (names) -> st.addNames (names)
    esac
  }, st, defs)
}

fun addArgs (st, names, vals) {
  case [names, vals] of
    [n:ns, v:vs] -> addArgs (st.addName (n, v), ns, vs)
  | _ -> st
  esac
}

(* Assignment *)
fun eval (c@[st, w], expr) {
  case expr of
    Assn  (e1, e2) -> 
      case eval (c, e1) of
        [c, Ref (s)] ->
          case eval (c, e2) of
            [[st, w], v] -> [[st <- [s, v], w], v]
          esac
      esac
  | Seq   (e1, e2) ->
      case eval (c, e1) of
        [c, _] -> eval (c, e2)
      esac
  | Skip      -> [c, Void]
  | Read  (s) -> 
      case readWorld (w) of
        [v, w] -> [[st <- [s, v], w], Void]
      esac
  | Write (e) ->
      case eval (c, e) of
        [[st, w], v] -> [[st, writeWorld (v, w)], Void]
      esac
  | If    (e, s1, s2) ->
      case eval (c, e) of
        [c, v] -> if v then eval (c, s1) else eval (c, s2) fi
      esac
  | While (e, s) ->
      case eval (c, e) of
        [c, v] -> if v then eval (c, Seq (s, expr)) else [c, Void] fi
      esac
  | DoWhile (s, e) -> eval (c, Seq (s, While (e, s)))
  | Var   (s) -> [c, st.lookup (s)]
  | Ref   (s) -> [c, expr]
  | Const (n) -> [c, n]
  | Binop (op, e1, e2) ->
      case eval (c, e1) of
        [c, v1] ->
          case eval (c, e2) of
            [c, v2] -> [c, evalOp (op, v1, v2)]
          esac
      esac
  | Scope (defs, e) ->
      case addDefs (st.enterScope, defs) of
        st -> 
          case eval ([st, w], e) of
            [[st, w], v] -> [[st.leaveScope, w], v]
          esac
      esac
  | Call (s, es) ->
      case c.evalList (es) of
        [[st, w], vals] ->
          case st.lookup (s) of
            Fun (args, body) ->
              case addArgs (st.enterFunction, args, vals) of
                stF -> 
                  case eval ([stF, w], body) of
                    [[stF, w], ret] -> [[st.leaveFunction (stF.getGlobal), w], ret]
                  esac
              esac
          esac
      esac
  | Ignore (e) -> eval (c, e)
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
