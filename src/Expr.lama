-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of
    Var   (varName)         -> st(varName)
  | Const (value)           -> value
  | Binop (op, left, right) -> evalBinop(st, op, left, right)
  esac
}

public fun evalBinop (st, op, left, right) {
  applyBinop(op, evalExpr(st, left), evalExpr(st,right))
}

public fun applyBinop (op, left, right) {
  case op of
    "+" -> left + right
  | "-" -> left - right
  | "*" -> left * right
  | "/" -> left / right
  | "%" -> left % right

  | "==" -> left == right
  | "!=" -> left != right

  | "<" -> left < right
  | "<=" -> left <= right
  | ">" -> left > right
  | ">=" -> left >= right

  | "&&" -> left && right
  | "!!" -> left !! right
  esac
}
