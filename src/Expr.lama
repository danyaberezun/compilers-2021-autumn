-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun op (s_op, l, r) {
  case s_op of
    "+" -> l + r
  | "-" -> l - r
  | "*" -> l * r 
  | "/" -> l / r 
  | "%" -> l % r
  | "==" -> l == r
  | "!=" -> l != r
  | ">" -> l > r
  | "<" -> l < r
  | ">=" -> l >= r
  | "<=" -> l <= r
  | "&&" -> l && r
  | "!!" -> l !! r
  esac
}

public fun evalExpr (st, expr) {
  case expr of
    Binop (s_op, expr1, expr2) -> op(s_op, evalExpr(st, expr1), evalExpr(st, expr2))
  | Var (s_var) -> st(s_var)
  | Const (n) -> n
  esac
}