-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[st, w], expr) {
  case expr of
    Skip                               -> [c, Unit]
  | Var     (x)                        -> [c, st(x)]
  | Ref     (x)                        -> [c, x]
  | Const   (n)                        -> [c, n]
  | Assn    (x, expr)                  ->
      case evalList (c, {x, expr}) of
        [[st, w], {x, v}] -> [[st <- [x, v], w], v]
      esac
  | Seq     (left, right)              ->
      case evalList (c, {left, right}) of
        [c, {_, v}] -> [c, v]
      esac
  | Read    (x)                        ->
      case readWorld (w) of
        [n, w] -> [[st <- [x, n], w], Unit]
      esac
  | Write   (expr)                     ->
      case eval (c, expr) of
        [[st, w], n] -> case writeWorld (n, w) of
          w -> [[st, w], Unit]
        esac
      esac
  | If      (cond, thenBody, elseBody) ->
      case eval (c, cond) of
        [c, n] -> eval (c, if n then thenBody else elseBody fi)
      esac
  | While   (cond, body)               -> eval (c, If (cond, Seq (body, expr), Skip))
  | DoWhile (body, cond)               -> eval (c, Seq (body, While (cond, body)))
  | Binop   (op, left, right)          ->
      case evalList (c, {left, right}) of
        [c, {l, r}] -> [c, evalOp (op, l, r)]
      esac
  | Ignore  (expr)                     ->
      case eval (c, expr) of
        [c, _] -> [c, Unit]
      esac
  | _                                  -> failure ("Uknown expression: %s\n", expr.string)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
