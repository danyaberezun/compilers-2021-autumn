-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn (l, r) -> 
      case evalList (c, {l, r}) of
        [[ns, nw], {lv, rv}] -> [[ns <- [lv, rv], nw], rv]
      esac
  | Seq (l, r) -> 
    case evalList (c, {l, r}) of 
      [nc, {lv, rv}] -> [nc, rv]
    esac 
  | Skip -> [c, None]
  | Read    (v)             ->
    case readWorld (w) of
      [x, w] -> [[s <- [v, x], w], None]
    esac
  | Write   (e)             ->
    case eval (c, e) of
      [[s, w], e] -> [[s, writeWorld (e, w)], None]
    esac
  | If (cond, l, r) -> 
    case eval (c, cond) of 
      [nc, 0] -> eval (nc, r)
    | [nc, 1] -> eval (nc, l)
    esac
  | While (cond, body) -> eval (c, If (cond, Seq (body, While (cond, body)), Skip))
  | DoWhile (body, cond) -> eval (c, Seq (body, While (cond, body)))
  | Var (v) -> [c, s (v)]
  | Ref (v) -> [c, v]
  | Const (v) -> [c, v]
  | Binop (sign, f, s) ->
    case evalList (c, {f, s}) of
      [nc, {lv, rv}] -> [nc, evalOp (sign, lv, rv)]
    esac
  | Ignore (e) ->
    case eval (c, e) of
      [nc, _] -> [nc, None]
    esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
