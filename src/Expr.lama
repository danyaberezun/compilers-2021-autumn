-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (cfg@[state, world], expr) {
  -- printf("In eval, expr: %s\n", expr.string);
  -- printf("In eval, world: %s\n", world.string);
  case expr of
    Read (Ref(x)) ->  case readWorld(world) of [res, world] -> 
                -- printf("In read got: %s\n", res.string);
                [[state <-[x, res], world], {}]
      esac 
  | Write (x) -> case eval(cfg, x) of [[state, world], {x}] -> 
        -- printf("In write got %s\n", x.string);
        [[state, writeWorld(x, world)], {}]
      esac
  | Seq (left, right) -> evalList(cfg, {Ignore(left), right})
  | Ref (x) -> [cfg, {x}]
  | Var (x) -> [cfg, {state(x)}]
  | Ignore (e) -> case eval(cfg, e) of [cfg, _] -> [cfg, {}] esac
  | Assn (x, e) -> case eval(cfg, x) of [cfg, {x}] -> 
        case eval(cfg, e) of [[state, world], {v}] -> 
          [[state <- [x, v], world], {v}]
      esac esac
  | Const (n) -> [cfg, {n}]
  | Binop (op, l, r) -> case eval(cfg, l) of [[state, world], {l}] -> 
        case eval(cfg, r) of [[state, world], {r}] -> 
          [cfg, {evalOp(op, l, r)}]
        esac
      esac
  | While (e, s) -> case eval(cfg, e) of [cfg, {v}] -> 
        if v then 
          case eval(cfg, s) of [cfg, _] -> eval(cfg, expr) esac
        else
          [cfg, {}]
        fi
      esac
  | If (e, s1, s2) -> case eval(cfg, e) of [cfg, {v}] -> 
        if v then 
          eval(cfg, s1)
        else 
          eval(cfg, s2)
        fi
      esac
  | Skip -> [cfg, {}]
    
  | expr -> failure ("evalExpr is unimplemented for %s\n", expr.string)
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
