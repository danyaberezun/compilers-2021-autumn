-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Const (x) -> [c, x]
  | Var (x) -> [c, s (x)]
  | Ref (x) -> [c, x]
  | Binop (op, lhs, rhs) -> 
    case evalList (c, {lhs, rhs}) of 
      [c, {lhs, rhs}] -> [c, evalOp (op, lhs, rhs)]
    esac 
  | Skip -> [c, Void]
  | Assn (l, r) ->
    case evalList (c, {l, r}) of
      [[s, w], {rx, v}] -> [[s <- [rx, v], w], v]
    esac
  | Read (x) ->
    case readWorld (w) of
      [value, newWorld] -> [[s <- [x, value], newWorld], Void]
    esac
  | Write (e) ->
    case eval (c, e) of
      [[s, w], v] -> [[s, writeWorld (v, w)], Void]
    esac
  | Seq (s1, s2) -> 
    case evalList (c, {s1, s2}) of 
      [c, {_, ans}] -> [c, ans]
    esac
  | If (cond, s1, s2) ->
    case eval (c, cond) of
      [c, 0] -> eval (c, s2)
    | [c, n] -> eval (c, s1)
    esac
  | While (cond, s) ->
    case eval (c, cond) of
        [c, 0] -> [c, Void]
      | [c, n] -> eval (c, Seq (s, expr))
    esac 
  | DoWhile (s, cond) -> 
    case eval (c, s) of 
      [c, v] -> eval (c, While (cond, s))
    esac
  | Ignore (x) -> 
    case eval (c, x) of 
      [c, _] -> [c, Void]  
    esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
