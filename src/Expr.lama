-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun binop (op) {
  case op of
    "+"  -> infix +
  | "-"  -> infix -
  | "*"  -> infix *
  | "/"  -> infix /
  | "%"  -> infix %
  | "==" -> infix ==
  | "!=" -> infix !=
  | "<=" -> infix <=
  | "<"  -> infix <
  | ">=" -> infix >=
  | ">"  -> infix >
  | "!!" -> infix !!
  | "&&" -> infix &&
  esac
}

public fun evalExpr (st, expr) {
  case expr of
    Var   (x)        -> st (x)
  | Const (n)        -> n
  | Binop (op, x, y) -> binop (op) (evalExpr (st, x), evalExpr (st, y))
  esac
}
