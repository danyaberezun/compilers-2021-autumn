-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

fun checkArgsCount (name, expected, actual) {
  if actual != expected
  then error (sprintf ("wrong number of arguments in call of function ""%s""", name), getLoc (name))
  fi
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

fun addNamesWithVals (state, names, values) {
  foldl (fun (s, [name, value]) {addName (s, name, Val (value))}, state, zip (names, values))
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Skip                   -> [c, 0]
  | Ref      (name)        -> [c, Ref (name)]
  | Var      (name)        -> [c, case lookupVal (s, name) of Val (value) -> value esac]
  | Const    (value)       -> [c, value]
  | String   (s)           -> [c, s]
  | Array    (es)          -> case evalList (c, es) of
                                [c, vs] -> [c, listArray (vs)]
                              esac
  | Sexp     (name, es)    -> case evalList (c, es) of
                                [c, vs] -> [c, Sexp (name, listArray (vs))]
                              esac
  | Binop    (op, l, r)    -> case evalList (c, {l, r}) of
                                [c, {w, v}] -> [c, evalOp (op, w, v)]
                              esac
  | Assn     (l, r)        -> case evalList (c, {l, r}) of
                                [[s, w], {Ref (x), v}]   -> [[s <- [x, Val (v)], w], v]
                              | [c, {ElemRef (l, i), v}] -> l[i] := v; [c, v]
                              esac
  | Set      (name, e)     -> case eval (c, e) of
                                [[s, w], v] -> [[s <- [name, Val (v)], w], v]
                              esac
  | Seq      (s1, s2)      -> case eval (c, s1) of
                                [c, _] -> eval (c, s2)
                              esac
  | If       (e, s1, s2)   -> case eval (c, e) of
                                [c, n] -> if n != 0 then eval (c, s1) else eval (c, s2) fi
                              esac
  | wh@While (e, s)        -> case eval (c, e) of
                                [c, n] -> if n != 0 then case eval (c, s) of [c, v] -> eval (c, wh) esac else [c, 0] fi
                              esac
  | DoWhile  (s, e)        -> eval (c, Seq (s, While (e, s)))
  | Scope    (ds, e)       -> var stateWithDefs = foldl (fun (newState, def) {
                                                            case def of
                                                              Val (names) -> addNames (newState, names)
                                                            | Var (names) -> addNames (newState, names)
                                                            | Fun (name, args, body) -> addFunction (newState, name, args, body)
                                                            esac
                                                         }, enterScope (s), ds);
                              case eval ([stateWithDefs, w], e) of
                                [[s, w], v] -> [[leaveScope (s), w], v]
                              esac
  | Call     (name, es)    -> case evalList (c, es) of
                                [[s1, w], args] ->
                                  case lookupFun (s, name) of
                                    Fun (params, External) -> eval ([s1, w], Builtin (name, args))
                                  | Fun (params, body)     -> checkArgsCount (name, size (params), size (args));
                                                              case eval ([addNamesWithVals (enterFunction (s1), params, args), w], body) of
                                                                [[s2, w], v] -> [[leaveFunction (s1, getGlobal (s2)), w], v]
                                                              esac
                                  esac
                              esac
  | Elem     (l, idx)      -> case evalList (c, {l, idx}) of
                                [c, {Sexp (_, l), i}] -> [c, l[i]]
                              | [c, {l, i}]           -> [c, l[i]]
                              esac
  | ElemRef  (l, idx)      -> case evalList (c, {l, idx}) of
                                [c, {Sexp (_, l), i}] -> [c, ElemRef (l, i)]
                              | [c, {l, i}]           -> [c, ElemRef (l, i)]
                              esac
  | Builtin  (name, vs)    -> case evalBuiltin (name, vs, w) of
                                [v, w] -> [[s, w], v]
                              esac
  | Ignore   (e)           -> case eval (c, e) of [c, _] -> [c, 0] esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
