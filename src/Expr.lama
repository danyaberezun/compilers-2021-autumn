-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Sexp    (string, expr list)  |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {

  fun applyAssign(c, lvalue, rvalue) {
    case evalList(c, {lvalue, rvalue}) of 
        [c, {to, from}] -> case to of
            Ref(name) -> case c of 
                [s, w] -> [[s <- [name, from], w], from]
            esac
          | ElemRef(arr, index) -> (
            arr[index] := from;
            [[s, w], from])  
        esac 
    esac
  }

  fun applySeq(c, first, second) {
    case evalList(c, {first, second}) of 
        [c, {_, result}] -> [c, result]
    esac
  }

  fun applyBinop(c, op, lhs, rhs) {
    case evalList(c, {lhs, rhs}) of 
        [c, {lhsRes, rhsRes}] -> [c, evalOp(op, lhsRes, rhsRes)]
    esac 
  }

  fun applyIf(c, condition, thenExpr, elseExpr) {
    case eval(c, condition) of 
        [c, flag] -> if flag then 
                         eval(c, thenExpr)
                     else
                         eval(c, elseExpr)
                     fi
    esac
  }

  fun applyWhile(c@[st, world], condition, body) {
    case eval(c, condition) of 
        [c, flag] -> if flag then 
                         case eval(c, body) of
                              [c, _] -> applyWhile(c, condition, body)
                         esac
                     else
                         [c, None]
                     fi
    esac
  }

  fun applyDoWhile(c, condition, body) {
    case eval(c, body) of
        [c, _] -> applyWhile(c, condition, body)
    esac
  }

  fun applyIgnore(c, expr) {
    case eval(c, expr) of 
        [c, _] -> [c, None]
    esac
  }

  fun evalInScope(c@[s, w], defs, expr) {

    fun constructScope(s, defs) {
      foldr (fun(s, def) {
        case def of 
            Var(names)            -> addNames(s, names)
          | Fun(name, args, body) -> addFunction(s, name, args, body)  
        esac
      }, enterScope(s), defs)
    }

    case defs of 
        {} -> eval(c, expr)
      | _  -> case eval([constructScope(s, defs), w], expr) of 
                  [[s, w], v] -> [[leaveScope(s), w], v] 
              esac
    esac
  } 

  fun applyBuiltin(c@[s, w], name, args) {
    case evalList(c, args) of 
      [[s1, w1], vals] -> 
        case evalBuiltin(name, vals, w1) of
            [value, w] -> [[s1, w], value] 
        esac
    esac
  }

  fun applyArray(c@[s, w], exprs) {
    case evalList(c, exprs) of 
      [[s, w], vals] -> [[s, w], listArray(vals)]
    esac
  }

  fun applyString(c, literal) {
    [c, literal]
  }

  fun applySexp(c@[s, w], tag, children) {
    case evalList(c, children) of 
      [[s, w], vals] -> [[s, w], Sexp(tag, listArray(vals))]
    esac
  }
  
  fun applyElem(c, arr, index) {
    case evalList(c, {arr, index}) of 
         [c, {Sexp(tag, children), indexValue}] -> [c, children[indexValue]]
      |  [c, {arrValue, indexValue}]            -> [c, arrValue[indexValue]] 
    esac
  }

  fun applyElemRef(c, arr, index) {
    case evalList(c, {arr, index}) of 
         [c, {Sexp(tag, children), indexValue}] -> [c, ElemRef(children, indexValue)]
      |  [c, {arrValue, indexValue}]            -> [c, ElemRef(arrValue, indexValue)] 
    esac
  }

  fun applyCall(c@[s, w], name, args) {
    fun addArgs(s, args, vals) {
      foldr ( fun (s, [arg, value]) {
        addName(s, arg, value)
      }, s, zip(args, vals))
    }

    -- printf("call %s on {%s}\n", name, args.string);

    case lookup(s, name) of 
        Fun(formalArgs, External) -> applyBuiltin(c, name, args) 
      | Fun(formalArgs, body) -> 
          case evalList(c, args) of 
            [[s1, w1], vals] -> 
              case eval([addArgs(enterFunction(s), formalArgs, vals), w], body) of 
                [[s2, w2], res] -> [[leaveFunction(s1, getGlobal(s2)), w2], res]
              esac    
          esac 
    esac  
  } 

  -- printf("`%s`\n", expr.string);

  case expr of
      Const(value)                       -> [c, value]
    | Var(name)                          -> [c, lookup(s, name)]
    | Ref(name)                          -> [c, Ref(name)]
    | Binop(op, lhs, rhs)                -> applyBinop(c, op, lhs, rhs)
    | Skip                               -> [c, None]
    | Assn (lvalue, rvalue)              -> applyAssign(c, lvalue, rvalue)
    | Set (lvalue, rvalue)               -> applyAssign(c, Ref(lvalue), rvalue)
    | Seq(first, second)                 -> applySeq(c, first, second)
    | If (condition, thenExpr, elseExpr) -> applyIf(c, condition, thenExpr, elseExpr)
    | While (condition, body)            -> applyWhile(c, condition, body)
    | DoWhile (body, condition)          -> applyDoWhile(c, condition, body)
    | Ignore (expr)                      -> applyIgnore(c, expr)
    | Scope (defs, expr)                 -> evalInScope(c, defs, expr)
    | Call (name, args)                  -> applyCall(c, name, args)
    | Builtin (name, args)               -> applyBuiltin(c, name, args)
    | Array(exprs)                       -> applyArray(c, exprs)
    | String (literal)                   -> applyString(c, literal)
    | Elem (arr, index)                  -> applyElem(c, arr, index)
    | ElemRef (arr, index)               -> applyElemRef(c, arr, index)
    | Sexp (tag, elems)                  -> applySexp(c, tag, elems) 
    | _                                  -> failure("Oops!")
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
