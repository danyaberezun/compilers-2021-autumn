-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

fun addArgs (state, names, values) {
  foldl (fun (s, [name, value]) {s.addName (name, Var (value))}, state, zip (names, values))
}

fun assignVar (state, name, value) {
  safeGetVar (state, name); state <- [name, Var (value)]
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

fun addDefs (state, ds) {
  foldl (fun (s, d) {
    case d of 
      Var (names)            -> addNames (s, names)
    | Fun (name, args, body) -> addFunction (s, name, args, body)
    | _                      -> failure ("Uknown definition: %s\n", d.string)
    esac
  }, state, ds)
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[st, w], expr) {
  case expr of
    Skip                         -> [c, Unit]
  | Var     (x)                  -> [c, safeGetVar (st, x)]
  | Ref     (x)                  -> [c, x]
  | Const   (n)                  -> [c, n]
  | Assn    (x, expr)            ->
      case evalList (c, {x, expr}) of
        [[st, w], {x, v}] -> [[assignVar (st, x, v), w], v]
      esac
  | Seq     (left, right)        ->
      case evalList (c, {left, right}) of
        [c, {_, v}] -> [c, v]
      esac
  | Read    (x)                  ->
      case readWorld (w) of
        [v, w] -> [[assignVar (st, x, v), w], Unit]
      esac
  | Write   (expr)               ->
      case eval (c, expr) of
        [[st, w], n] -> case writeWorld (n, w) of
          w -> [[st, w], Unit]
        esac
      esac
  | If      (cond, tBody, eBody) ->
      case eval (c, cond) of
        [c, n] -> eval (c, if n then tBody else eBody fi)
      esac
  | While   (cond, body)               -> eval (c, If (cond, Seq (body, expr), Skip))
  | DoWhile (body, cond)               -> eval (c, Seq (body, While (cond, body)))
  | Binop   (op, left, right)          ->
      case evalList (c, {left, right}) of
        [c, {l, r}] -> [c, evalOp (op, l, r)]
      esac
  | Call    (name, args)         ->
      case safeGetFun (st, name) of
        Fun (argNames, body) -> 
          if size (argNames) != size (args)
          then error (sprintf ("wrong number of arguments for ""%s"", extected %d, found %d", name, size (argNames), size (args)), getLoc (name))
          else
            case evalList (c, args) of
              [[st, w], vs] -> case addArgs (enterFunction (st), argNames, vs) of
                stFun -> case eval ([stFun, w], body) of
                  [[stFun, w], v] -> [[leaveFunction (st, getGlobal (stFun)), w], v]
              esac
            esac
          esac
        fi
      esac
  | Scope   (defs, expr)         ->
      case enterScope (st) of
        st -> case addDefs (st, defs) of
          st -> case eval ([st, w], expr) of
            [[st, w], n] -> case leaveScope (st) of
              st -> [[st, w], n]
            esac
          esac
        esac
      esac
  | Ignore  (expr)               ->
      case eval (c, expr) of
        [c, _] -> [c, Unit]
      esac
  | _                                  -> failure ("Uknown expression: %s\n", expr.string)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
