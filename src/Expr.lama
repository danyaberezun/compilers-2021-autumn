-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        Scope (def list, expr)       |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun addDefs (state, defs) {
  foldl (fun (state, df) {
    case df of 
      Var (vars) -> addNames(state, vars)
      | Fun(name, args, body) -> addFunction(state, name, args, body)
    esac 
  }, state, defs)
}

fun passArgs (state, args, vals) {
  foldl (fun (state, [arg_, val_]) {
    addName(state, arg_, val_)
  }, state, zip(args, vals))
}

fun eval (c@[s, w], expr) {
  case expr of 
    Ref (x) -> [c, Ref (x)] 
    | Ignore (e) -> case eval(c, e) of [c@[s, w], _] -> [c, Bottom] esac
    | Var (x) -> [c, lookup(s, x)]
    | Const (n) -> [c, Val (n)]
    | Skip -> [c, Bottom]
    | Set (x, expr) -> case eval(c, expr) of 
        [c@[s, w], v] -> [[s <- [x, v], w], v]
      esac
    | Assn (x, v) -> case eval(c, x) of 
        [c@[s, w], x] -> case eval(c, v) of 
          [c@[s, w], Val (v)] -> case x of 
            Ref (x) -> [[s <- [x, Val(v)], w], Val(v)]
            | ElemRef (arr@#str, ind) -> arr[ind] := v; [c, Val(v)]
            | ElemRef (arr@#array, ind) -> arr[ind] := Val(v); [c, Val (v)]
            | ElemRef (Sexp(_, vals), ind) -> vals[ind] := Val(v); [c, Val(v)]
          esac
        esac
      esac
    | Seq (s1, s2) -> case eval(c, s1) of 
        [c@[s, w], _] -> eval(c, s2)
      esac
    | If (cond, thenExpr, elseExpr) -> case eval(c, cond) of 
        [c@[s, w], Val (0)] -> eval(c, elseExpr)
        | [c@[s, w], _] -> eval(c, thenExpr)
      esac
    | While (cond, body) -> case eval(c, cond) of 
        [c@[s, w], Val (0)] -> [c, Bottom]
        | [c@[s, w], _] -> case eval(c, body) of 
          [c@[s, w], _] -> eval(c, While (cond, body))
        esac
      esac 
    | DoWhile (body, cond) -> case eval(c, body) of 
        [c@[s, w], _] -> eval(c, While (cond, body))
      esac 
    | Binop (op, s1, s2) -> case eval(c, s1) of 
        [c@[s, w], Val (r1)] -> case eval(c, s2) of 
          [c@[s, w], Val (r2)] -> [c, Val (evalOp(op, r1, r2))]
        esac 
      esac 
    | Call (f, args_epxr) -> case lookupFun(s, f) of
        Fun (args, External) -> case evalList(c, args_epxr) of 
          [c@[s, w], vals] -> case evalBuiltin(f, foldr(fun (acc, Val (x)) { x : acc }, {}, vals), w) of
            [v, w] -> [[s, w], Val (v)]
          esac 
        esac 
        | Fun (args, body) -> case evalList(c, args_epxr) of 
          [c@[s, w], vals] -> 
            case eval([passArgs(enterFunction(s), args, vals), w], body) of 
              [[fun_s, w], r] -> [[leaveFunction(s, getGlobal(fun_s)), w], r]
            esac 
        esac
      esac
    | Scope (defs, expr) ->  case eval([addDefs(enterScope(s), defs), w], expr) of
        [c@[s, w], r] -> [[leaveScope(s), w], r]
      esac
    | String (s) -> [c, Val (s)] 
    | Array (vals_list) -> case evalList(c, vals_list) of 
        [c@[s, w], vals] -> [c, Val (listArray(vals))]
      esac 
    | Sexp (name, vals_list) -> case evalList(c, vals_list) of 
        [c@[s, w], vals] -> [c, Val (Sexp(name, listArray(vals)))]
      esac
    | Elem (arr_expr, ind_expr) -> case eval(c, arr_expr) of 
        [c, Val (arr)] -> case eval(c, ind_expr) of 
          [c@[s, w], Val (ind)] -> case arr of 
            #str -> [c, Val (arr[ind])] 
            | #array -> [c, arr[ind]]
            | Sexp(_, vals) -> [c, vals[ind]]
          esac
        esac 
      esac
    | ElemRef (arr_expr, ind_expr) -> case eval(c, arr_expr) of 
        [c, Val (arr)] -> case eval(c, ind_expr) of 
          [c@[s, w], Val (ind)] -> [c, ElemRef (arr, ind)]
        esac 
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
