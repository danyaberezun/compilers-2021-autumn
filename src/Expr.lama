-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), "meme")-- getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun checkSexp(e) {
  case e of
    Sexp (_, arr) -> arr
  | _ -> e
  esac
}

fun eval (c@[s, w], expr) {
--   printf("Expr log: %s\n", string(expr));
  case expr of
    Assn (s1, s2) ->
      case evalList (c, {s1, s2}) of
        [[s, w], { ref, n }] ->
          case ref of
            Ref (x) -> [[s <- [x, n], w], n]
          | ElemRef (arr, idx) -> checkSexp(arr)[idx] := n; [[s, w], n]
          esac
      esac
  | Seq (s1, s2) ->
      case evalList(c, {s1, s2}) of
        [c, {_, r}] -> [c, r]
      esac
  | Skip -> [c, Bottom]
  | If (e, s1, s2) ->
      case eval(c, e) of
        [c, n] -> if n then eval (c, s1) else eval (c, s2) fi
      esac
  | While (e, s) ->
      while case eval (c, e) of [newC, b] -> c := newC; b esac do
        c := fst (eval (c, s))
      od;
      [c, Bottom]
  | DoWhile (s, e) ->
      do
        c := fst (eval (c, s))
      while case eval (c, e) of [newC, b] -> c := newC; b esac od;
      [c, Bottom]
  | Var (x) ->
      [c, lookup (s, x)]
  | r@Ref (x) ->
      [c, r]
  | Const (x) -> [c, x]
  | Binop (op, s1, s2) ->
      case evalList (c, { s1, s2 }) of
        [c, {l, r}] ->
          [c, evalOp (op, l, r)]
      esac
  | Scope (defs, es) ->
      case enterScope (s) of
        s ->
          s := foldl (fun(s, e) {
            case e of
              Var (names) ->
--                 printf("Names added: %s\n", string(names));
                addNames (s, names)
            | Fun (name, args, body) -> addFunction(s, name, args, body)
            esac
          }, s, defs);
          case eval ([s, w], es) of
            [[s, w], v] -> [[leaveScope(s), w], v]
          esac
      esac
  | Call (f, args) ->
      lookupFun (s, f);
      case evalList (c, args) of
        [[s, w], args] ->
          case lookup (s, f) of
            Fun (argNames, body) ->
              case body of
                External -> case evalBuiltin(f, args, w) of [v, w] -> [[s, w], v] esac
              | _ ->
                  var funS = foldl(fun (s, [name, v]) { addName(s, name, v) }, enterFunction (s), zip (argNames, args));
                  case eval ([funS, w], body) of
                    [[funS, w], v] -> [[leaveFunction (s, getGlobal (funS)), w], v]
                  esac
              esac
          esac
      esac
  | Ignore (e) ->
    case eval (c, e) of
      [c, _] -> [c, Bottom]
    esac
  | String (s) -> [c, s]
  | Array (es) ->
      case evalList(c, es) of
        [c, vals] -> [c, listArray(vals)]
      esac
  | Sexp (name, es) ->
      case evalList(c, es) of
        [c, vals] -> [c, Sexp (name, listArray(vals))]
      esac
  | Elem (s1, s2) ->
      case evalList(c, {s1, s2}) of
        [c, {arr, idx}] ->
          [c, checkSexp(arr)[idx]]
      esac
  | ElemRef (s1, s2) ->
      case evalList(c, {s1, s2}) of
        [c, {arr, idx}] -> [c, ElemRef (arr, idx)]
      esac
  | Builtin (name, es) ->
      case evalList(c, es) of
        [[s, w], args] ->
          case evalBuiltin(name, args, w) of
            [v, w] -> [[s, w], v]
          esac
      esac
  | e -> failure("unknown expr: " ++ string (e) ++ "\n")
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
