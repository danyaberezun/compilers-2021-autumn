-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        Repeat  (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, vl] -> [c, vl : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun matches (c, x) {
  fun compareLists(xs, ys) {
    if xs.size != ys.size then false 
    else foldl (fun (acc, [x, y]) {
                  if acc then matches (x, y) else false fi
                }, true, zip (xs, ys)
        )
    fi
  }
  case x of
    Wildcard      -> 1
  | Named  (_, x) -> matches (c, x)
  | Const  (n)    -> case c of
      #val -> c == n
    | _    -> 0
    esac
  | String (s)    ->case c of
      #str -> compare (c, s) == 0
    | _    -> false
    esac
  | Array (vals)  -> case c of
      Array (c)  -> compareLists (arrayList (c), arrayList (vals))
    | _            -> false
    esac
  | Sexp (name, vals) -> case c of 
      Sexp (s, xs) -> (compare (s, name) == 0) && compareLists (arrayList (xs), vals)
    | _            -> false
    esac
  esac
}

fun eval (c@[st, w], expr) {
  case expr of
    Assn  (x, v) -> case eval (c, x) of
      [c, xx] -> case eval (c, v) of
        [[st, w], vv] -> case xx of
          ElemRef (xx, ii) -> xx[ii] := vv;
          [[st, w], vv]
        | _ -> [[st <- [xx, vv], w], vv]
        esac
      esac
    esac
  | Var   (v) -> [c, lookup (st, v)]
  | Const (x) -> [c, x]
  | Binop (op, x, y) -> case eval (c, x) of
      [c, a] -> case eval (c, y) of
          [c, b] -> [c, evalOp (op, a, b)]
        esac
    esac
  | Ref   (x) -> [c, x]
  | Seq   (x, y) ->
      case eval (c, x) of
        [c, _] -> eval (c, y)
      esac
  | Skip      -> [c, Void]
  | Ignore  (e)  -> eval (c, e)
  | Read (x) -> 
        case eval(c, x) of
          [[s, w], Ref(x)] ->
            case readWorld(w) of
              [v, nW] -> [[s <- [x, v], nW], Void]
            esac
        esac
  | Write (e) -> 
        case eval(c, e) of
          [[s, w], Val(v)] -> [[s, writeWorld(v, w)], Void]
        esac
  | If (cond, a, b) -> 
      case eval(c, cond) of
        [c, v] -> 
          if v
          then
            eval(c, a)
          else
            eval(c, b)
          fi
      esac
  | While (e, s) -> case eval (c, e) of
      [c, v] -> if v 
      then eval (c, Seq (s, expr))
      else [c, Void] fi
    esac
  | DoWhile (s, e) -> eval (c, Seq (s, While (e, s)))
  | Scope (d, e) ->
    fun addScope (st, defs) {
      foldl (fun (st, d) {
        case d of
          Fun (name, args, body) -> addFunction (st, name, args, body)
        | Var (names) -> addNames (st, names)
        | Val (names) -> addNames (st, names)
        esac
      }, st, defs)
    }
      case addScope (enterScope (st), d) of
        st -> case eval ([st, w], e) of
          [[st, w], v] -> [[leaveScope (st), w], v]
        esac
      esac
  | Call (f, es) ->
      case evalList (c, es) of
        [[st, w], vals] ->
          case st.lookup(f) of
            Fun (args, body) ->
            fun addParams (st, names, vals) {
              case [names, vals] of
                [n:ns, v:vs] -> addParams (addName (st, n, v), ns, vs)
              | _ -> st
              esac
            }
              case body of
                External -> case evalBuiltin (f, vals, w) of
                              [v, w] -> [[st, w], v]
                            esac
              | _        -> var stF = addParams (enterFunction (st), args, vals);
                            case eval ([stF, w], body) of
                              [[stF, w], ret] -> [[leaveFunction (st, stF.getGlobal), w], ret]
                            esac
              esac
          esac
      esac
  | String (s)  -> [c, s]
  | Set (s, e)   -> eval (c, Assn (Ref (s), e))
  | Elem (x, i) -> case evalList (c, {x, i}) of
      [c, {xx, ii}] ->
        case xx of
          Sexp (s, xx) -> [c, xx[ii]]
        | _             -> [c, xx[ii]]
        esac
    esac
  | ElemRef (x, i) -> case evalList (c, {x, i}) of
      [c, {xx, ii}] -> case xx of
          Sexp (s, xx) -> [c, ElemRef (xx, ii)]
        | _             -> [c, ElemRef (xx, ii)]
        esac
    esac
  | Array (xs) -> case evalList (c, xs) of
      [c, xxs] -> [c, listArray (xxs)]
    esac
  | Sexp (s, xs) -> case evalList (c, xs) of
      [c, xxs] -> [c, Sexp (s, listArray (xxs))]
    esac
  | Case (s, branches) -> case branches of
      [x, b]:bs -> if   matches (lookup (st, s), x)
                   then eval (c, b)
                   else eval (c, Case (s, bs))
                   fi
    esac      
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
