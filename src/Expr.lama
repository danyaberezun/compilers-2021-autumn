-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
     Const (n) -> [c, n]
  |  Var (x) -> [c, s(x)]
  |  Ref (x) -> [c, x]
  |  Binop (op, e1, e2) ->
        case evalList(c, {e1,e2}) of 
          [c, v1:v2:_] -> [c, evalOp(op, v1, v2)]
        esac
  |  Assn (ref, e) ->
        case evalList(c, {ref, e}) of 
          [[s, w], x:v:_] -> [[s <- [x, v], w], v]
        esac
  |  Seq (e1, e2) ->
        case evalList(c, {e1, e2}) of 
          [c, v1:v2:t] -> [c, v2]
        esac
  |  Skip -> [c, Void]
  |  Read (x) -> 
    case readWorld(w) of 
      [v, w_prime] -> [[s <- [x, v], w_prime], Void]
    esac
  |  Write (e) -> 
    case eval(c, e) of 
      [c, v] -> [[s, writeWorld(v, w)], Void]
    esac
  |  If (cond, e1_, e2_) ->
    case eval(c, cond) of 
      [c, 0] -> eval(c, e2_) |
      [c, _] -> eval(c, e1_)
    esac
  |  While (cond, e_) ->
    case eval(c, cond) of
      [c, 0] -> [c, Void] |
      [c, _] -> 
        case eval(c, e_) of 
          [c, _] -> eval(c, While (cond, e_))
        esac
    esac
  |  DoWhile (e_, cond) -> 
    case eval(c, e_) of 
      [c, _] -> eval(c, While (cond, e_))
    esac
  |  Ignore (e) -> 
    case eval(c, e) of 
      [c, v] -> [c, Void]
    esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
