-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, world], expr) {
  case expr of
    Var (name) -> [c, s (name)]
    | Const (n) -> [c, n]
    | Ref (exp) -> [c, exp]
    | Binop (operation, first, second) ->
      case evalList (c, {first, second}) of
        [c, {first, second}] -> [c, evalOp (operation, first, second)]
      esac
    | Ignore (exp) ->
      case eval (c, exp) of
        [c, _] -> [c, {}]
      esac
    | Skip -> [c, {}]
    | Assn (x, exp) ->
      case evalList (c, {x, exp}) of
        [[s, world], {x, exp}] -> [[s <- [x, exp], world], exp]
      esac
    | Read (x) ->
      case readWorld (world) of
        [tmp, world] -> [ [s <- [x, tmp], world], {}]
      esac
    | Write (expr) ->
      case eval (c, expr) of
        [[s, world], tmp] -> [[s, writeWorld (tmp, world)], {}]
      esac
    | If (cond, ifBody, elseBody) ->
      case eval (c, cond) of [c, res] ->
        if res
            then eval (c, ifBody)
            else eval (c, elseBody)
        fi
      esac
    | DoWhile (cond, doWhileBody) -> eval (c, Seq (cond, While (doWhileBody, cond)))
    | While (cond, whileBody) ->
      case eval (c, cond) of [c, res] ->
        if res
            then eval (c, Seq (whileBody, While (cond, whileBody)))
            else [c, {}]
        fi
      esac
    | Seq (exp1, exp2) ->
      case evalList (c, {exp1, exp2}) of
        [c, {exp1, exp2}] -> [c, exp2]
      esac
    esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
