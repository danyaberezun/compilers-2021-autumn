-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of
    Var   (x)               -> st (x)
  | Const (n)               -> n
  | Binop (op, left, right) -> getOp (op) (evalExpr (st, left), evalExpr (st, right))
  | _                       -> failure ("Uknown expr: %s\n", expr.string)
  esac
}

public fun getOp (op) {
  case op of
    "+"  -> infix +
  | "-"  -> infix -
  | "*"  -> infix *
  | "/"  -> infix /
  | "%"  -> infix %
  | "==" -> infix ==
  | "!=" -> infix !=
  | "<"  -> infix <
  | "<=" -> infix <=
  | ">"  -> infix >
  | ">=" -> infix >=
  | "&&" -> infix &&
  | "!!" -> infix !!
  esac
}

