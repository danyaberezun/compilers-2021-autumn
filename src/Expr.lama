-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of 
    Var (x)              -> [c, s(x)]
  | Const (num)          -> [c, num]
  | Binop (op, lhs, rhs) ->
    case evalList(c, {lhs, rhs}) of
      [c2, {lhv, rhv}] -> [c2, evalOp(op, lhv, rhv)]
    esac
  | Write (e) ->
    case eval(c, e) of
      [[s2, w2], res] -> [[s2, writeWorld(res, w2)], Bot]
    esac
  | Read (x) ->
    case readWorld(w) of
      [res, w2] -> [[s <- [x, res], w2], Bot]
    esac
  | Seq (e1, e2) ->
    case eval(c, e1) of
      [c2, _] -> eval(c2, e2)
    esac
  | Ignor (e) -> [eval(c, e).fst, Bot]
  | Assn (l, r) ->
    case evalList(c, {l, r}) of
      [[s2, w2], {RefVal (x), v}] -> [[s2 <- [x, v], w2], v]
    esac
  | Ref (x) -> [c, RefVal (x)]
  | Skip -> [c, Bot]
  | While (e, s) ->
    case eval(c, e) of
      [c2, 0] -> [c2, Bot]
    | [c2, _] ->
        case eval(c2, s) of 
          [c3, v] -> eval(c3, expr)
        esac
    esac
  | DoWhile (s, e) -> eval(c, Seq(s, While(e, s)))
  | If (e, tru, fls) ->
      case eval(c, e) of
        [c2, 0] -> eval(c2, fls)
      | [c2, _] -> eval(c2, tru)
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
