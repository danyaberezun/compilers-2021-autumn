-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, [input, output]], expr) {
  case expr of
      Binop(op, e1, e2) ->
        case evalList(c, {e1, e2}) of
          [c1, {v1, v2}] -> [c1, evalOp(op, v1, v2)]
        esac
    | Const(n) -> [c, n]
    | Skip -> [c, None]
    | Read(x) -> 
      case input of
        h : input -> [[s <- [x, h], [input, output]], None]
      esac
    | Var(x) -> [c, s(x)]
    | Write(e) ->
      case eval(c, e) of
        [c1, v] -> [[s, [input, v : output]], None]
      esac
    | Ignore(e) ->
      case eval(c, e) of
        [c1, _] -> [c1, None]
      esac
    | Assn(e1, e2) ->
      case evalList(c, {e1, e2}) of
        [[s, w], {x, v}] -> [[s <- [x, v], w], v]
      esac
    | Ref(x) -> [c, x]
    | Seq(e1, e2) ->
      case eval(c, e1) of
        [c1, _] -> eval(c1, e2)
      esac
    | While(e1, e2) ->
      case eval(c, e1) of
          [c1, 0] -> [c1, None]
        | [c1, _] -> eval(eval(c1, e2).fst, While(e1, e2))
      esac
    | DoWhile(e1, e2) ->
      case eval(c, e1) of
          [c1, _] -> eval(c1, While(e2, e1))
      esac
    | If(e1, e2, e3) ->
      case eval(c, e1) of
          [c1, 0] -> eval(c1, e3)
        | [c1, _] -> eval(c1, e2)
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
