-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
    case expr of
      Const (n) -> [c, Val (n)]
    | Var (x) -> [c, Val (s(x))]
    | Ref (x) -> [c, Ref (x)]
    | Binop (op, l, r) -> case evalList(c, {l, r}) of
        [c, {Val (v1), Val (v2)}] -> [c, Val (evalOp(op, v1, v2))]
      esac
    | Skip -> [c, Void]
    | Assn (x, e) -> case evalList(c, {x, e}) of
      [[s, w], {Ref (x), Val (v)}] -> [[s <- [x, v], w], Val (v)]
    esac
    | Read (x) -> case readWorld(w) of
        [value, new_world] -> [[s <- [x, value], new_world], Void]
      esac
    | Write (e) -> case eval (c, e) of
        [[s, w], Val (v)] -> [[s, writeWorld(v, w)], Void]
      esac
    | Seq (l, r) ->
      case evalList(c, {l, r}) of
        [c, {v1, v2}] -> [c, v2]
      esac
    | Ignore (e) -> case eval (c, e) of
        [c, v] -> [c, Void]
      esac
    | While (e, s) -> case eval (c, e) of
        [c, Val (0)] ->  [c, Void]
      | [c, Val (n)] -> case eval (c, s) of
          [c, x] -> eval(c, While (e, s))
        esac
      esac
    | If (e, s1, s2) -> case eval (c, e) of
        [c, Val (0)] -> eval(c, s2)
      | [c, Val (n)] -> eval(c, s1)
      esac
    | DoWhile (s, e) -> case eval(c, s) of
        [c, x] -> eval(c, While(e, s))
      esac
    esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
