-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Skip -> [c, Z]
  | Ignore (ex) ->
      case eval (c, ex) of
        [c, _] -> [c, Z]
      esac
  | Seq (lhs, rhs) ->
      case evalList (c, {lhs, rhs}) of
        [c, {lhs, rhs}] -> [c, rhs]
      esac
  | Assn (lhs, rhs) ->
      case evalList (c, {lhs, rhs}) of
        [[s, w], {lhs, rhs}] -> [ [s <- [lhs, rhs], w], rhs ]
      esac
  | Ref (ex) -> [c, ex]
  | Var (name) -> [c, s (name)]
  | Const (x) -> [c, x]
  | Binop (op, lhs, rhs) ->
      case evalList (c, {lhs, rhs}) of
        [c, {lhs, rhs}] -> [c, evalOp (op, lhs, rhs)]
      esac
  | If (cond, bodyThen, bodyElse) ->
      case eval (c, cond) of [c, condVal] ->
        if condVal
        then eval (c, bodyThen)
        else eval (c, bodyElse)
        fi
      esac
  | lp@While (cond, body) ->
      case eval (c, cond) of [c, condVal] ->
        if condVal
        then eval (c, Seq (body, lp))
        else [c, Z]
        fi
      esac
  | DoWhile (cond, body) -> eval (c, Seq (body, While (cond, body)))
  | Read (name) ->
      case readWorld (w) of
        [x, w] -> [ [s <- [name, x], w], Z ]
      esac
  | Write (ex) ->
      case eval (c, ex) of
        [[s, w], x] -> [ [s, writeWorld (x, w)], Z ]
      esac
  esac
}
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)
--   failure ("evalExpr not implemented\n")
-- }


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
