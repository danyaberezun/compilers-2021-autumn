-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNamesConf ([state, world], names, values) {
  [foldl (fun (s, [name, value]) {s.addName (name, value)}, state, zip(names, values)), world]
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun enterScopeConf (c@[s, w]) {
  [enterScope(s), w]
}

fun leaveScopeConf (c@[s, w]) {
  [leaveScope(s), w]
}

fun enterFunctionConf(c@[s, w]) {
  [enterFunction(s), w]
}

public fun unwrapSexp(v) {
  case v of
    Sexp(_, a) -> a
    | a -> a
  esac
}

fun eval (c@[st, w], e) {
  -- fprintf(stderr, "%s\n", e.string);
  case e of
      Const (n) -> [c, n]
    | Var (x)   -> [c, lookup(st, x)]
    | Ref (x)   -> [c, Ref (x)]
    | Binop (op, e1, e2) ->
      case evalList(c, {e1, e2}) of
        [c, {w, v}] -> [c, evalOp (op, w, v)]
      esac
    | Skip -> [c, Bottom]
    | Assn (name, expr) ->
      case evalList(c, {name, expr}) of
          [[st, w], {Ref (x), value}] -> [[st <- [x, value], w], value]
        | [[st, w], {ElemRef (arr, i), value}] -> [[st, w], arr[i] := value]
      esac
    | Read (name) ->
      case readWorld (w) of
        [z, w] -> [[st <- [name, z], w], Bottom]
      esac
    | Write (expr) ->
      case eval(c, expr) of
        [[st, w], value] -> [[st, writeWorld (value, w)], Bottom]
      esac
    | Seq (s1, s2) ->
      case evalList(c, {s1, s2}) of
        [c, {_, v2}] -> [c, v2]
      esac
    | If (e, s1, s2) ->
      case eval(c, e) of
        [c, 0] -> eval(c, s2)
      | [c, n] -> eval(c, s1)
      esac
    | While (body, s) ->
      case eval(c, body) of
        [c, 0] -> [c, Bottom]
      | [c, n] -> eval(c, Seq(s, e))
      esac
    | DoWhile (s, e) ->
      eval(eval(c, s)[0], While(e, s))
    | Ignore (e) -> [eval(c, e)[0], Bottom]
    | Scope(defs, exprs) ->
      case eval(foldl(fun ([acc_st, w], def) {
          [case def of
              Fun (name, args, body) -> addFunction(acc_st, name, args, body)
            | Var (name)             -> addName(acc_st, name, 0)
          esac, w]
        }, enterScopeConf(c), defs), exprs) of
        [c, v] -> [leaveScopeConf(c), v]
      esac
    | Call (x, arg_vals) ->
      case lookup(st, x) of
        Fun(arg_names, body) ->
          case evalList(c, arg_vals) of
            [new_c, arg_vals] ->
              case eval(addNamesConf(enterFunctionConf(new_c), arg_names, arg_vals), body) of
                [[c, w], value] -> [[leaveFunction(new_c[0], getGlobal(c)), w], value]
              esac
          esac
      esac
    | Builtin(x, arg_vals) ->
      case evalList(c, arg_vals) of
        [c, arg_vals] ->
        case evalBuiltin(x, arg_vals, w) of
          [v, w] -> [[st, w], v]
        esac
      esac
    | String(s) -> [c, s]
    | Sexp(s, vals) ->
      case evalList(c, vals) of
        [c, vals] -> [c, Sexp(s, listArray(vals))]
      esac
    | Array(vals) ->
      case evalList(c, vals) of
        [c, vals] -> [c, listArray(vals)]
      esac
    | Elem(e, i) ->
      case evalList(c, {e, i}) of
        [c, {e, i}] -> [c, unwrapSexp(e)[i]]
      esac
    | ElemRef(e, i) ->
      case evalList(c, {e, i}) of
        [c, {e, i}] -> [c, ElemRef(unwrapSexp(e), i)]
      esac

  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
