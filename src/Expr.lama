-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Sexp    (string, expr list)  |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, 0)}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

fun addDefs (state, defs) {
  foldl(
    fun (st, def) {
      case def of
          Fun (name, args, body) -> addFunction(st, name, args, body)
        | Var (vars) -> addNames(st, vars)
      esac
    },
    state,
    defs
  )
}

fun addVals (state, pairs) {
  foldl(
    fun (st, [ x, v ]) {
      addName (st, x, v)
    },
    state,
    pairs
  )
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (
    fun ([c, vals], e) {
      case eval (c, e) of
        [c, v] ->
          -- printf("evalList: c=%s, v=%s, vals=%s\n", c.string, v.string, vals.string);
          [c, v : vals]
      esac
    },
    [c, {}],
    exprs
  ) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[state, world], expr) {
  -- c = [ state, world, ], where
  --   state = [ (scope, mapping), ... ] - stack of scopes and mappings between names and values
  --   world - just world (stdin/stdout)
  -- printf("Inside eval: expr=%s\n", expr.string);

  case expr of
    Const (n) -> [c, n]
  | Var (x) -> [c, lookup (state, x)]
  | Ref (x) -> [c, x]
  | Binop (op, e1, e2) ->
      case evalList (c, { e1, e2 }) of
        [c, arr@{ l, r }] -> 
          -- printf("binop l=%s, r=%s, op=%s, evaluated=%s, arr=%s\n", l.string, r.string, op.string, evalOp (op, l, r).string, arr.string); 
          [c, evalOp (op, l, r)]
      esac
  | Skip -> [c, {}]
  | Set (x, e) ->
      case eval(c, e) of
        [[st, wrld], v] -> [[st <- [x, v], wrld], v] 
      esac
  | Assn (e1, e2) ->
      case evalList (c, { e1, e2 }) of
        [[st, wrld], arr@{ x, v }] ->
          -- printf("Assign: arr=%s\n", arr.string);
          -- printf("Assn (%s, %s): state=%s\n", x.string, v.string, st.string);
          case x of
            ElemRef (arr, i) ->
              arr[i] := v;
              [[st, wrld], v]
          | _ -> [[st <- [x, v], wrld], v]
          esac
      esac
  | Ignore (e) ->
      case eval (c, e) of
        [conf, v] -> [conf, {}]
      esac
  | Seq (e1, e2) -> eval(eval(c, e1).fst, e2)
  | If (e, s1, s2) ->
      case eval(c, e) of
        [conf, 0] -> eval(conf, s2)
      | [conf, _] -> eval(conf, s1)
      esac
  | While (e, s) ->
      case eval(c, e) of
        [conf, 0] -> [conf, {}]
      | [conf, _] -> eval(eval(conf, s).fst, expr)
      esac
  | DoWhile (s, e) -> eval(c, Seq (s, While (e, s)))
  | Scope (defs, innerExpr) -> 
      case eval([addDefs(enterScope(state), defs), world], innerExpr) of
        [[st, w], v] -> [[leaveScope(st), w], v]
      esac
  | Call (name, params) -> 
      lookupFun (state, name);
      case lookup (state, name) of
        Fun (args, body) -> 
          case evalList (c, params) of
            -- assign vals to args
            [[st, w], vals] ->
              case body of
                External ->
                  case evalBuiltin (name, vals, w) of
                    [v, wrld] -> [ [st, wrld], v ]
                  esac
              | _ ->
                case eval (
                  [addVals (enterFunction (st), zip (args, vals)), w],
                  body
                ) of
                  [[afterCallSt, w], v] -> [[leaveFunction (st, getGlobal (afterCallSt)), w], v]
                esac
              esac
          esac
      esac
  | Array (exprs) ->
      case evalList (c, exprs) of
        [conf, vals] -> [conf, listArray (vals)]
      esac
  | String (s) -> [c, s]
  | Sexp (tag, exprs) ->
      case evalList (c, exprs) of
        [conf, data] -> [conf, Sexp (tag, listArray (data))]
      esac
  | Elem (e1, e2) ->
      case evalList (c, { e1, e2 }) of
        [conf, Sexp (tag, data) : i : _] -> [conf, data[i]]
      | [conf, arr : i : _] -> [conf, arr[i]] 
      esac
  | ElemRef (e1, e2) -> 
      case evalList (c, { e1, e2 }) of
        [conf, arr : i : _] -> [conf, ElemRef (arr, i)] 
      esac
  | x -> failure ("evalExpr for '%s' not implemented\n", x.string)
  esac

  -- failure ("evalExpr not implemented\n")
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope
    .addName ("read",   Fun ({}, External))
    .addName ("write",  Fun ({"a"}, External))
    .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr)
  of
    [c, _] -> c.snd.getOutput
  esac
}
