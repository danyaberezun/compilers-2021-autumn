-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun evalScope(defs, s) {
  case defs of
    def:tail -> case def of
        Var (names) -> evalScope(tail, addNames(s, names))
      | Fun (name, args, body) -> evalScope(tail, addFunction(s, name, args, body))
    esac
    | _ -> s
  esac
}

fun evalArgs(args, s) {
  case args of
      [argName, argValue]:tail -> evalArgs(tail, addName(s, argName, argValue))
    | _ -> s
  esac
}

(* Assignment *)
fun eval (c@[s, w], expr) {
  case expr of
      Const (n) -> [c, Val (n)]
    | Var (x) -> [c, s.lookup(x)]
    | Ref (x) -> [c, Ref (x)]
    | Binop (op, l, r) -> case evalList(c, {l, r}) of
        [c, {Val (v1), Val (v2)}] -> [c, Val (evalOp(op, v1, v2))]
      esac
    | Skip -> [c, Void]
    | Assn (x, e) -> case evalList(c, {x, e}) of
      [[s, w], {Ref (x), v}] -> [[s <- [x, v], w], v]
    esac
    | Read (x) -> case readWorld(w) of
        [value, new_world] -> [[s <- [x, Val(value)], new_world], Void]
      esac
    | Write (e) -> case eval (c, e) of
        [[s, w], Val (v)] -> [[s, writeWorld(v, w)], Void]
      esac
    | Seq (l, r) ->
      case evalList(c, {l, r}) of
        [c, {v1, v2}] -> [c, v2]
      esac
    | Ignore (e) -> case eval (c, e) of
        [c, v] -> [c, Void]
      esac
    | While (e, s) -> case eval (c, e) of
        [c, Val (0)] ->  [c, Void]
      | [c, Val (n)] -> case eval (c, s) of
          [c, x] -> eval(c, While (e, s))
        esac
      esac
    | If (e, s1, s2) -> case eval (c, e) of
        [c, Val (0)] -> eval(c, s2)
      | [c, Val (n)] -> eval(c, s1)
      esac
    | DoWhile (s, e) -> case eval(c, s) of
        [c, x] -> eval(c, While(e, s))
      esac
    | Scope (defs, e) -> case evalScope(defs, s.enterScope) of
        s -> case eval([s, w], e) of
          [[s, w], v] -> [[s.leaveScope, w], v]
        esac
      esac
    | Call (name, args) -> case s.lookup(name) of
      Fun (argsNames, body) -> case evalList(c, args) of
        [[s1, w], argsValues] -> case eval([evalArgs(zip(argsNames, argsValues), s1.enterFunction), w], body) of
          [[s2, w], v] -> [[s1.leaveFunction(s2.getGlobal), w], v]
        esac
      esac
    esac
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}