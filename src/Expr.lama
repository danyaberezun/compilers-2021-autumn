-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}
fun addDef(state, def) {
  case def of
    Fun (name, params, body)  -> addFunction(state, name, params, body)
  | Var (names)               -> addNames(state, names)
  esac
}

fun addArg(state, [argName, argValue]) {
  addName(state, argName, argValue)
}

fun unboxList(vals) {
  case vals of 
    Val(v) : rest -> v : unboxList(rest)
  | x : rest -> failure("Cannot unbox %s\n", x) 
  | emp -> emp  
  esac
}

fun boxList(vals) {
  case vals of 
    v : rest -> Val(v) : boxList(rest)
  | emp -> emp  
  esac
}

fun eval (config@[state, world], expr) {
  case expr of 
    Const(value) -> [config, Val(value)]
  | Var(name) -> [config, Val(state.lookup(name))]
  | Ref(name) -> [config, Ref(name)]
  | Binop(op, lhs, rhs) -> 
    case evalList(config, {lhs, rhs}) of 
      [config, {Val(lhs), Val(rhs)}] -> [config, Val(evalOp (op, lhs, rhs))]
    esac
  | Skip ->  [config, Bot]
  | Set (name, expr) -> eval(config, Assn(Ref(name), expr))
  | Assn (lhs, rhs) ->
    case evalList(config, {lhs, rhs}) of 
      [config@[state, world], {Ref (var_name), Val(rhs)}] -> [[state <- [var_name, rhs], world], Val(rhs)]
    | [config@[state, world], {ERCalc(arr, idx), Val(rhs)}] -> 
    (
      arr[idx] := rhs;
      [config, Val(arr[idx])]
    )
    esac
  | Seq (left, right) -> 
    case evalList(config, {left, right}) of 
      [config, {left, right}] -> [config, right]
    esac
  | If (cond, then_block, else_block) ->
    case eval(config, cond) of 
      [config, Val(0)] -> eval(config, else_block)
    | [config, _] -> eval(config, then_block)
    esac
  | While (cond, body) -> 
    case eval(config, cond) of 
      [config, Val(0)] -> [config, Bot]
    | [config, _] -> (
      case eval(config, body) of 
        [config, _] -> eval(config, While(cond, body))
      esac
    )
    esac
  | DoWhile (body, cond) -> 
    case eval(config, body) of 
      [config, Bot] -> 
        case eval(config, cond) of 
          [config, Val(0)] -> [config, Bot]
        | [config, _] -> eval (config, DoWhile(body, cond))
        esac
    esac
  | Ignore (expr) ->
    case eval(config, expr) of 
      [config, _] -> [config, Bot]
    esac
  | Scope (defs, expr) -> 
    case eval([foldl(addDef, enterScope(state), defs), world], expr) of
      [[state, world], expr] -> [[leaveScope(state), world], expr]
    esac
  | Call (funName, exprs) -> 
    case evalList(config, exprs) of
      [config@[saved@state, world], params] ->
        case lookup(state, funName) of 
          Fun(paramNames, body) -> (
            case body of 
              External -> 
                case evalBuiltin(funName, unboxList(params), world) of
                  [response, world] -> [[state, world], Val(response)]
                esac
            | _ ->
                case eval([foldl(addArg, enterFunction(state), zip(paramNames, unboxList(params))), world], body) of 
                  [config@[state, world], result] -> [[leaveFunction(saved, getGlobal(state)), world], result] 
                esac
            esac
          )
        esac 
    esac
  | String (s) -> [config, Val(s)]
  | Elem (arr, idx) ->
    case evalList(config, {arr, idx}) of 
      [config, {Val(ds), Val(idx2)}] -> (
        case ds of 
          Sexp(name, arr2) -> [config, Val(arr2[idx2])]
        | arr2 -> [config, Val(arr2[idx2])]
        esac
      )
    | _ -> failure("Cannot interpret elem: %s %s", arr.string, idx.string)
    esac
  | ElemRef (arr, idx) ->
    case evalList(config, {arr, idx}) of 
      [config, {Val(ds), Val(idx2)}] ->  
        case ds of 
          Sexp(name, arr2) -> [config, ERCalc(arr2, idx2)]
        | arr2 -> [config, ERCalc(arr2, idx2)]
        esac
    | _ -> failure("Cannot interpret elem: %s %s", arr.string, idx.string)
    esac
  | Array (exprs) -> 
    case evalList(config, exprs) of 
      [config, exprs] -> [config, Val(listArray(unboxList(exprs)))]
    esac
  | Sexp (name, exprs) ->
    case evalList(config, exprs) of 
      [config, exprs] -> [config, Val(Sexp(name, listArray(unboxList(exprs))))]
    esac
  esac

}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}