-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (l, op, r) { Binop (op, l, r) }

-- Primary expression
var primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                 x=lident  {Var (x)}               |
                                 inbr[s("("), exp, s(")")]),
      exp = memo $ eta expr (
          {
            [Left, {[s ("!!"), binop ]}],
            [Left, {[s ("&&"), binop ]}],

            [Nona, {[s ("=="), binop],
                    [s ("!="), binop],
                    [s (">"),  binop],
                    [s ("<"),  binop],
                    [s (">="), binop],
                    [s ("<="), binop]}],

            [Left, {[s ("+"), binop],
                    [s ("-"), binop]}],

            [Left, {[s ("*"), binop],
                    [s ("/"), binop],
                    [s ("%"), binop]}]
          }
          , primary
        );
      -- exp = primary;

var simpleStmt = memo $ eta syntax (
    kRead x=inbr[s("("), lident, s(")")] { Read (x) } |
    kSkip { Skip } |
    x=lident s[":="] e=exp { Assn (x, e) } |
    kWrite e=inbr[s("("), exp, s(")")] { Write (e) }
  );

var stmtCtrl = memo $ eta syntax (
      kIf e=exp kThen s1=stmt kElse s2=stmt kFi { If(e,s1,s2) } |
      kIf e=exp kThen s1=stmt kFi {If(e,s1, Skip)} |
      kIf e=exp kThen s1=stmt s2=stmtElif kFi {If(e,s1,s2)} |

      kWhile e=exp kDo s=stmt kOd { While(e,s) } |
      kDo s=stmt kWhile e=exp kOd {DoWhile(e, s)} |

      kFor s1=stmt s[","] e=exp s[","] s2=stmt kDo s3=stmt kOd {Seq(s1, While(e, Seq(s3, s2)))}
  );

var stmtElif = memo $ eta syntax (
  kElif e=exp kThen s1=stmt {If(e, s1, Skip)} |
  kElif e=exp kThen s1=stmt kElse s2=stmt {If(e, s1, s2)} |
  kElif e=exp kThen s1=stmt s2=stmtElif {If(e, s1, s2)} 
  );

var stmtBlock = memo $ eta syntax ( simpleStmt | stmtCtrl );
var stmt = memo $ eta syntax (
        stmtBlock |
        s1=stmtBlock s[";"] s2=stmt { Seq(s1, s2) } 
  );

   

-- Public top-level parser
public parse = stmt;
             
