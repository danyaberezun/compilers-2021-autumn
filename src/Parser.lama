-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- Primary expression
var primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                 x=lident  {Var (x)}               |
                                 inbr[s("("), exp, s(")")]),
      exp = memo $ eta expr ({
        [Right, {
          [s(":="), fun(l, op, r) {Assn(l, r)}]
        }],
        [Left, {
          [s("!!"), fun(l, op, r) {Binop(op, l, r)}]
        }],
        [Left, {
          [s("&&"), fun(l, op, r) {Binop(op, l, r)}]
        }],
        [Nona, {
          [s("=="), fun(l, op, r) {Binop(op, l, r)}],
          [s("!="), fun(l, op, r) {Binop(op, l, r)}],
          [s("<="), fun(l, op, r) {Binop(op, l, r)}],
          [s("<"), fun(l, op, r) {Binop(op, l, r)}],
          [s(">"), fun(l, op, r) {Binop(op, l, r)}],
          [s(">="), fun(l, op, r) {Binop(op, l, r)}]
        }],
        [Left, {
          [s("+"), fun(l, op, r) {Binop(op, l, r)}],
          [s("-"), fun(l, op, r) {Binop(op, l, r)}]
        }],
        [Left, {
          [s("*"), fun(l, op, r) {Binop(op, l, r)}],
          [s("/"), fun(l, op, r) {Binop(op, l, r)}],
          [s("%"), fun(l, op, r) {Binop(op, l, r)}]
        }]
    }, primary);

var elsePart = memo $ eta syntax (
  kElse s2=stmt {s2} |
  kElif e=exp kThen s=stmt {If(e, s, Skip)} |
  kElif e=exp kThen s=stmt el=elsePart {If(e, s, el)}
);

var assn = memo $ eta syntax (
  name=lident s[":="] e=exp {Assn(name, e)}
);

var line = memo $ eta syntax (
  kRead name=inbr[s("("), lident, s(")")] {Read(name)} |
  kWrite e=inbr[s("("), exp, s(")")] {Write(e)} |
  kSkip {Skip} |
  name=lident s[":="] e=exp {Assn(name, e)} |
  kIf e1=exp kThen s1=stmt el=elsePart kFi {If(e1, s1, el)} |
  kIf e1=exp kThen s1=stmt kFi {If(e1, s1, Skip)} |
  kWhile e=exp kDo s=stmt kOd {While(e, s)} |
  kDo s=stmt kWhile e=exp kOd {DoWhile(s, e)} |
  kFor assn=stmt s[","] cond=exp s[","] step=stmt kDo s=stmt kOd {Seq(assn, While(cond, Seq(s, step)))}
);

var stmt = memo $ eta syntax (
  line |
  head=line s[";"] tail=stmt {Seq(head, tail)}
);


-- Public top-level parser
public parse = stmt;