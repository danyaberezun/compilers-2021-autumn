-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var elifPat = memo $ eta syntax ( kElif xi=exp kThen yi=exp { [xi, yi] } );

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  loc=pos kRead x=inbr[s("("), lident, s(")")]      {fun (a) {assertVoid (a, Read (x), loc)}}                    |
                                  loc=pos kWrite e=inbr[s("("), exp, s(")")]        {fun (a) {assertVoid (a, Write (e (Val)), loc)}}             |
                                  e=inbr[s("("), exp, s(")")]                       {e}                                                          |
                                  loc=pos kWhile x=exp kDo y=exp kOd                {fun (a) {assertVoid (a, While (x (Val), y (Void)), loc)}}   |
                                  loc=pos kIf x1=exp kThen y1=exp a=(elifPat)* b=(kElse yLast=exp { yLast })? kFi {
                                      fun (attr) {
                                        var assert = fun (x1, x2, x3) { x2 };
                                        var start =
                                          case b of
                                            Some (y) -> If (x1 (Val), y1 (attr), y (attr))
                                          | _ -> assert := assertVoid; If (x1 (Val), y1 (Void), Skip)
                                          esac;
                                        var result = foldl (
                                          fun (acc, x) {
                                            case acc of
                                              If (e, s1, s2) -> If (e, s1, If (x[0] (Val), x[1] (attr), s2))
                                            esac
                                          }
                                        , start, a);
                                        assert (attr, result, loc)
                                      }
                                    } |
                                  loc=pos kDo y=exp kWhile x=exp kOd                 {fun (a) {assertVoid (a, DoWhile (y (Void), x (Val)), loc)}} |
                                  loc=pos kSkip                                      {fun (a) {assertVoid (a, Skip, loc)}}                        |
                                  loc=pos kFor s1=exp s[","] e=exp s[","] s2=exp kDo s3=exp kOd {
                                      fun (a) {
                                        var result = Seq (
                                          s1 (Void),
                                          While (e (Val), Seq (s3 (Void), s2 (Void)))
                                        );
                                        assertVoid (a, result, loc)
                                      }
                                    }
                                  ),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
