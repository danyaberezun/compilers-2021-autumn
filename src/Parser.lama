-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- Primary expression
var primary = memo $ eta syntax (x=decimal {Const (stringInt (x))} |
                                 x=lident  {Var (x)}               |
                                 inbr[s("("), exp, s(")")]),

  exp = memo $ eta (expr({
    [Left, {[s("&&"), fun (l, op, r) { Binop("&&", l, r) }],
            [s("!!"), fun (l, op, r) { Binop("!!", l, r) }]}],
    [Nona, {[s("=="), fun (l, op, r) { Binop("==", l, r) }],
            [s("!="), fun (l, op, r) { Binop("!=", l, r) }],
            [s(">"),  fun (l, op, r) { Binop(">", l, r) }],
            [s(">="), fun (l, op, r) { Binop(">=", l, r) }],
            [s("<"),  fun (l, op, r) { Binop("<", l, r) }],
            [s("<="), fun (l, op, r) { Binop("<=", l, r) }]}],
    [Left, {[s("+"), fun (l, op, r) { Binop("+", l, r) }],
            [s("-"), fun (l, op, r) { Binop("-", l, r) }]}],
    [Left, {[s("*"), fun (l, op, r) { Binop("*", l, r) }],
            [s("/"), fun (l, op, r) { Binop("/", l, r) }],
            [s("%"), fun (l, op, r) { Binop("%", l, r) }]}]
    }, primary));

var if_stmt = memo $ eta syntax (
  e = exp kThen st = stmt { If(e, st, Skip) } |
  e = exp kThen s1 = stmt kElse s2 = stmt { If(e, s1, s2) } |
  e = exp kThen s1 = stmt kElif s2 = if_stmt { If(e, s1, s2) }
);

var one_st = memo $ eta syntax (x = inbr[s("("), stmt, s(")")] { x } |
                                kIf s = if_stmt kFi { s } |
                                kDo st = stmt kWhile e = exp kOd { DoWhile(st, e) } |
                                kWhile x = exp kDo st = stmt kOd { While(x, st) } |
                                kFor s1 = stmt s[","] e = exp s[","] s2 = stmt kDo s3 = stmt kOd { Seq(s1, While(e, Seq(s3, s2))) } |
                                kRead x = inbr[s("("), lident, s(")")] { Read(x) } | 
                                kWrite e =  inbr[s("("), exp, s(")")] { Write(e) } | 
                                kSkip { Skip } | 
                                x = lident s[":="] e = exp { Assn(x, e) });

var stmt = memo $ eta syntax (one_st | 
                              s1 = one_st s[";"] s2 = stmt { Seq(s1, s2) });

-- Public top-level parser
public parse = stmt;
