-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  kSkip { fun(a) {Skip} } |
                                  loc=pos kRead s["("] v=lident s[")"]   { fun(a) {  assertVoid(a, Read(v), loc) }} |
                                  loc=pos kWrite s["("] v=exp s[")"] { fun(a) { assertVoid(a, Write(v(Val)), loc) } } |
                                  kIf c=exp kThen body=exp kElse body_else=exp kFi { fun(a) { If(c(Val), body(a), body_else(a))} } |
                                  loc=pos kIf cond=exp kThen body=exp kElif elseBody=ifExprNoKeyword kFi {fun (a) {If (cond (Val), body (a), elseBody (a))}} |
                                  loc=pos kIf c=exp kThen body=exp kFi { fun(a) { assertVoid(a, If(c(Val), body(Void), Skip), loc)}} |
                                  loc=pos kWhile e=exp kDo body=exp kOd { fun(a) { assertVoid(a, While(e(Value), body(Void)), loc) }} |
                                  loc=pos kDo body=exp kWhile e=exp kOd { fun(a) { assertVoid(a, DoWhile(e(Value), body(Void)), loc) } } |
                                  loc=pos kFor init=exp s[","] cond=exp s[","] inc=exp kDo body=exp kOd { fun(a) { assertVoid(a, Seq (init(Void), While (cond(Val), Seq (body(Void), inc(Void)))), loc) }} |
                                  loc=pos s["("] x=exp s[")"] { fun (a) {x (a)} }
                                  -- loc=pos { error("unrecognized stuff", loc) } 
                                  ),
                                  -- $(failure ("the rest of primary parsing in not implemented\n"))),
    ifExprNoKeyword = memo $ eta syntax (
      x=exp kThen y=exp { fun (a) {If (x(Val), y(Void), Skip) } } |
      x=exp kThen y=exp kElse z=exp { fun(a) { If (x(Val), y (a), z (a)) } } |
      x=exp kThen y=exp kElif z=ifExprNoKeyword { fun(a) { If (x(Val), y(a), z(a)) } }
    ),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
