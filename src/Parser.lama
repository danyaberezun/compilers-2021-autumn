-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  loc=pos kRead variable=inbr[s("("), lident, s(")")] { fun (a) { assertVoid(a, Read(variable), loc) } } |
                                  loc=pos kWrite expression=inbr[s("("), basic, s(")")] { fun (a) { assertVoid(a, Write(expression (Val)), loc) } } |
                                  loc=pos expression=inbr[s("("), exp, s(")")] { fun (a) { expression (a)}} |
                                  loc=pos kSkip { fun (a) { assertVoid (a, Skip, loc) }} |
                                  loc=pos kWhile expression=basic kDo stmtWhile=exp kOd { fun (a) { assertVoid(a, While (expression (Val) , stmtWhile (Void)), loc) } } |
                                  loc=pos kDo stmtDoWhile=exp kWhile expression=basic kOd { fun (a) { assertVoid(a, DoWhile (expression (Val), stmtDoWhile (Void)), loc) } } |
                                  loc=pos kIf expression=basic kThen stmtIf=exp stmtElifElse=stmt_Elif_Else kFi { fun (a) { If(expression (Val), stmtIf (a), stmtElifElse (a)) } } |
                                  loc=pos kIf expression=basic kThen stmtIf=exp kFi { fun(a) { assertVoid(a, If(expression (Val), stmtIf (Void), Skip), loc) } } |
                                  loc=pos kFor stmt1=basic s[","] expression=basic s[","] stmt2=basic kDo stmtFor=exp kOd { fun (a) { assertVoid(a, Seq(stmt1 (Void), While(expression (Val), Seq(stmtFor (Void), stmt2 (Void)))), loc) } }
                                  ),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}}),
    stmt_Elif_Else = memo $ eta syntax (
      loc=pos kElif expression=basic kThen elif_stmt=basic elif_else_2=stmt_Elif_Else { fun (a) { If(expression (Val), elif_stmt (a), elif_else_2 (a)) }} |
      loc=pos kElse else_stmt=exp { fun (a) { else_stmt (a) }  }
    );

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
