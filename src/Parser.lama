-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}


var elif_exp_fi = memo $ eta syntax (kElse st=exp kFi                               {fun (a) {st (a)}} |
                                      kFi                                           {fun (a) {assertVoid  (a, Skip, loc)}} |
                                      kElif ex=exp kThen st1=exp st2=elif_exp_fi    {fun (a) {If(ex (Val), st1 (a), st2 (a))}}
                                     );

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  kSkip                                             {fun (a) {assertVoid(a, Skip, loc)}} |
                                  kRead x=inbr[s("("), lident, s(")")]              {fun (a) {assertVoid(a, Read (x), loc)}} |
                                  kWrite x=inbr[s("("), exp, s(")")]                {fun (a) {assertVoid(a, Write (x (Val)), loc)}} |
                                  kWhile ex=exp kDo st=exp kOd                      {fun (a) {assertVoid (a, While (ex (Val), st (Void)), loc)}} |
                                  kDo st=exp kWhile ex=exp kOd                      {fun (a) {assertVoid (a, DoWhile (st (Void), ex (Val)), loc)}} |
                                  kIf ex=exp kThen st1=exp st2=elif_exp_fi          {fun (a) {If(ex (Val), st1 (a), st2 (a))}} |
                                  kFor st0=exp s[","] ex=exp s[","] sti=exp kDo st=exp kOd {fun (a) {Seq(st0 (Void), While(ex (Val), Seq(st (Void), sti (Void))))}} |
                                  inbr[s("("), exp, s(")")]
                                  ),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
