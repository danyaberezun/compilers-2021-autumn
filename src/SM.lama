-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  case insns of
    {}        -> c
  | head:tail -> case evalInsn (env, c, head, tail) of [c, tail] -> eval (env, c, tail) esac
  esac
}

fun evalInsn (env, c@[stack, st, w], insn, tail) {
  case insn of
    READ           -> [case readWorld (w) of [value, w2] -> [value:stack, st, w2] esac, tail]
  | WRITE          -> [case stack of head:tail -> [tail, st, writeWorld (head, w)] esac, tail]
  | BINOP (op)     -> [case stack of x:y:tail  -> [binop (op) (y, x) : tail, st, w] esac, tail]
  | LD    (name)   -> [[st (name) : stack, st, w], tail]
  | ST    (name)   -> [case stack of head:tail -> [tail, st <- [name, head], w] esac, tail]
  | CONST (value)  -> [[value:stack, st, w], tail]
  | LABEL (name)   -> [c, tail]
  | JMP   (lab)    -> [c, fromLabel (env, lab)]
  | CJMP  (f, lab) -> case stack of
                        h:t -> if h != 0 && compare ("nz", f) == 0 !! h == 0 && compare ("z", f) == 0
                                 then [[t, st, w], fromLabel (env, lab)]
                                 else [[t, st, w], tail]
                               fi
                      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var   (name)            -> singletonBuffer (LD (name))
  | Const (value)           -> singletonBuffer (CONST (value))
  | Binop (op, left, right) -> compileExpr (left) <+> compileExpr (right) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (e, s1, s2) -> case genLabel (env) of [lab1, env] ->
                          case compile (lab, env, s1) of [_, env, code1] ->
                            case compile (lab, env, s2) of [_, env, code2] ->
                              [true, 
                                env, 
                                compileExpr (e) <+ CJMP ("z", lab1) <+> code1 <+ JMP (lab) 
                                      <+ LABEL (lab1) <+> code2 -- <+ JMP (lab)
                              ] 
                            esac
                          esac
                        esac
    | While (e, s)   -> case genLabels (env, 2) of [cond, st, env] ->
                          case compileExpr (e) of eCode ->
                            case compile (cond, env, s) of [_, env, sCode] ->
                              [false,
                               env,
                               singletonBuffer (JMP (cond)) 
                                       <+ LABEL (st) <+> sCode <+ LABEL (cond) <+> eCode
                                       <+ CJMP ("nz", st)
                              ]
                            esac
                          esac
                        esac
    | DoWhile (s, e) -> case genLabel (env) of [lab1, env] ->
                          case compileExpr (e) of eCode ->
                            case compile (lab1, env, s) of [_, env, sCode] ->
                              [false,
                               env,
                               singletonBuffer (LABEL (lab1)) <+> sCode <+> eCode <+ CJMP ("nz", lab1)
                              ]
                            esac
                          esac
                        esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
