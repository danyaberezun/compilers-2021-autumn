-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun shoundJump (flg, vl) {
  vl != 0
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  case insns of 
    {}         -> c
  | (i : nsns) ->
      case i of
        READ      -> 
          case c of
            [stk, st, w] -> 
              case readWorld(w) of 
                [fst, w] -> 
                  eval(env, 
                    [fst : stk, st, w], 
                  nsns)
              esac                  
          esac
      | WRITE     ->
          case c of
            [fst : stk, st, w] -> 
              eval(env, 
                [stk, st, writeWorld(fst, w)],
              nsns)
          esac
      | BINOP (s) -> 
          case c of
            [snd : (fst : stk), st, w] -> 
              eval(env, 
                [
                  case s of
                    "+" -> fst + snd 
                  | "-" -> fst - snd 
                  | "*" -> fst * snd 
                  | "/" -> fst / snd 
                  | "%" -> fst % snd 
                  | "<" -> fst < snd 
                  | "<=" -> fst <= snd 
                  | ">" -> fst > snd 
                  | ">=" -> fst >= snd 
                  | "==" -> fst == snd 
                  | "!=" -> fst != snd 
                  | "!!" -> fst !! snd 
                  | "&&" -> fst && snd 
                  esac
              : stk, st, w],
              nsns)
          esac
      | LD    (x) -> 
          case c of
            [stk, st, w] -> 
              eval(env, 
                [st(x) : stk, st, w],
              nsns)
          esac
      | ST    (x) ->
          case c of
            [fst : stk, st, w] -> 
              eval(env, 
                [stk, st <- [x, fst], w],
              nsns)
          esac
      | CONST (n) -> 
          case c of
            [stk, st, w] -> 
              eval(env, 
                [n : stk, st, w],
              nsns)
          esac
      | JMP (lab) ->
          eval(env, c, env.fromLabel(lab))
      | LABEL (lab) -> eval(env, c, nsns)
      | CJMP (flg, lab) ->
          case c of
            [cond : stk, st, w] -> 
              if shoundJump(flg, cond) then
                eval(env, [stk, st, w], env.fromLabel(lab))
              else
                eval(env, [stk, st, w], nsns)
              fi
          esac
      esac
    | _ -> c
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of 
    Var   (name) -> singletonBuffer (LD (name)) 
  | Const (value) -> singletonBuffer (CONST (value))
  | Binop (op, ex1, ex2) -> compileExpr(ex1) <+> compileExpr(ex2) <+> singletonBuffer (BINOP (op))
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | While (expr, sub_stmt) ->
        case env.genLabels(2) of
          [sLab, eLab, env] -> 
            case compile(sLab, env, sub_stmt) of
              [sLabUsed, env, subCode]  ->
                [false, env, singletonBuffer (JMP (eLab)) <+> label (sLab, true) <+> subCode <+> label (eLab, true) <+> compileExpr (expr) <+ CJMP("nz", sLab) ]
            esac
        esac
    | DoWhile (sub_stmt, expr) ->
        case compile(lab, env, sub_stmt) of
          [labUsed, env, subCode]  ->
            [labUsed, env, label (lab, true) <+> subCode <+> compileExpr (expr) <+ CJMP("nz", lab) ]
        esac
    | If (expr, stmt1, stmt2) ->
        case env.genLabels(3) of
          [s1Lab, s2Lab, fLab, env] ->
            case compile(s1Lab, env, stmt1) of
              [s1LabUsed, env, s1Code] ->
                case compile(s2Lab, env, stmt2) of
                  [s2LabUsed2, env, s2Code] ->
                    [false, env, compileExpr(expr) <+ CJMP("nz", s1Lab) <+> s2Code <+ JMP(fLab) <+> label(s1Lab, true) <+> s1Code <+> label(fLab, true)]
                esac
            esac
        esac
    | _              -> failure ("compileSM not implemented\n")
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
