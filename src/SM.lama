-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }
    
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  fun createLocal (stack, a, l) {
      case take (stack, a) of
        [stack, args] ->
          var state = makeState (a, l);
          foldl (fun (i, arg) {assign (state, Arg (i), arg); i + 1}, 0, args);
          [stack, state]
      esac
    }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (c@[stack, ctrlStack, state, world], insns) {
    case insns of
      {}           -> c
    | insn : insns ->
        case insn of
          BINOP (op)      ->
            case stack of
              x : y : stack -> eval ([evalOp (op, y, x) : stack, ctrlStack, state, world], insns)
            esac

        | LD (x)          -> eval ([lookup (state, x) : stack, ctrlStack, state, world], insns)

        | LDA (x)         -> eval ([Ref (x) : stack, ctrlStack, state, world], insns)

        | ST (x)          ->
            case stack of
              z : _ -> assign (state, x, z); eval ([stack, ctrlStack, state, world], insns)
            esac

        | SEXP (s, na)    ->
            case take (stack, na) of
              [stack, args] -> eval ([Sexp (s, listArray (args)) : stack, ctrlStack, state, world], insns)
            esac

        | CONST (z)       -> eval ([z : stack, ctrlStack, state, world], insns)

        | LABEL (l)       -> eval (c, insns)

        | JMP (l)         -> eval (c, fromLabel (env, l))

        | CJMP (c, l)     ->
            case c of
              "z"  -> case stack of
                        z : stack -> if z == 0
                                     then eval ([stack, ctrlStack, state, world], fromLabel (env, l))
                                     else eval ([stack, ctrlStack, state, world], insns)
                                     fi
                      esac
            | "nz" -> case stack of
                        z : stack -> if z == 0
                                     then eval ([stack, ctrlStack, state, world], insns)
                                     else eval ([stack, ctrlStack, state, world], fromLabel (env, l))
                                     fi
                      esac
            esac

        | DROP            ->
            case stack of
              _ : stack -> eval ([stack, ctrlStack, state, world], insns)
            esac

        | GLOBAL (x)      -> assign (state, Glb (x), 0); eval ([stack, ctrlStack, state, world], insns)

        | BEGIN (_, a, l) ->
            case createLocal (stack, a, l) of
              [stack, state] -> eval ([stack, ctrlStack, state, world], insns)
            esac

        | END             ->
            case ctrlStack of
              {}                         -> c
            | [state, insns] : ctrlStack -> eval ([stack, ctrlStack, state, world], insns)
            esac

        | CALL (f, na)    ->
            case f[0] of
              '$' ->
                case take (stack, na) of
                  [stack, args] ->
                    var fname = substring (f, 1, length (f) - 1); -- cut $
                    case evalBuiltin (fname, args, world) of
                      [v, world] -> eval ([v : stack, ctrlStack, state, world], insns)
                    esac
                esac
            | _ -> eval ([stack, [state, insns] : ctrlStack, state, world], fromLabel (env, f))
            esac

        | STRING (s)      -> eval ([s : stack, ctrlStack, state, world], insns)

        | ARRAY (n)       ->
            case take (stack, n) of
              [stack, a] -> eval ([listArray(a) : stack, ctrlStack, state, world], insns)
            esac

        | STA             ->
           case stack of
             -- 1st branch is previous STI
             v : Ref (x) : Ref (_) : stack -> assign (state, x, v); eval ([v : stack, ctrlStack, state, world], insns)
           | v : i : Sexp (_, s)   : stack -> s[i] := v; eval ([v : stack, ctrlStack, state, world], insns)
           | v : i : a             : stack -> a[i] := v; eval ([v : stack, ctrlStack, state, world], insns)
           esac

        | ELEM            ->
            case stack of
              i : Sexp (_, s) : stack -> eval ([s[i] : stack, ctrlStack, state, world], insns)
            | i : a           : stack -> eval ([a[i] : stack, ctrlStack, state, world], insns)
            esac

        | DUP             ->
            case stack of
              x : _ -> eval ([x : stack, ctrlStack, state, world], insns)
            esac
        esac
    esac
  }

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }

  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }

  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

public fun compileSM (expr) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun registerDef ([env, funs, globals], def) {
    case def of
      Var (vs)               -> [addVars (env, vs),
                                 funs,
                                 (if isGlobal (env)
                                  then map (fun (name) {GLOBAL (name)}, vs)
                                  else {}
                                  fi) +++ globals]

    | Val (vs)               -> [addVals (env, vs),
                                 funs,
                                 (if isGlobal (env)
                                  then map (fun (name) {GLOBAL (name)}, vs)
                                  else {}
                                  fi) +++ globals]

    | Fun (name, args, body) ->
        case genFunLabel (env, name) of
          [fLabel, env] -> [addFun (env, name, fLabel, args.size), [fLabel, args, body] : funs, globals]
        esac
    esac
  }

  -- TODO: cases {e, es}, e : es looks very bad
  fun compileMany (lab, env, exprs) {
    case exprs of
      {e}     -> compile (lab, env, e)
    | {e, es} ->
        case genLabel (env) of
          [eLab, env] ->
            case compile (eLab, env, e) of
              [eUsed, env, eSM] ->
                case compileMany (lab, env, {es}) of
                  [labUsed, env, esSM] -> [labUsed, env, eSM <+> label (eLab, eUsed) <+> esSM]
                esac
            esac
        esac
    | e : es ->
       case genLabel (env) of
          [eLab, env] ->
            case compile (eLab, env, e) of
              [eUsed, env, eSM] ->
                case compileMany (lab, env, es) of
                  [labUsed, env, esSM] -> [labUsed, env, eSM <+> label (eLab, eUsed) <+> esSM]
                esac
            esac
        esac
    esac
  }

  fun compile (lab, env, stmt) {
    case stmt of
      Skip              -> [false, env, emptyBuffer ()]

    | Var    (x)        -> [false, env, singletonBuffer (LD (lookupVal (env, x)))]

    | Ref    (x)        -> [false, env, singletonBuffer (LDA (lookupVar (env, x))) <+ DUP]

    | Const  (n)        -> [false, env, singletonBuffer (CONST (n))]

    | Binop (op, l, r)  ->
        case compile (lab, env, l) of
          [_, env, lSM] ->
            case compile (lab, env, r) of
              [_, env, rSM] -> [false, env, lSM <+> rSM <+ BINOP (op)]
            esac
        esac

    | Assn (x, e) ->
        case compile (lab, env, x) of
          [_, env, xSM] ->
            case compile (lab, env, e) of
              [_, env, eSM] -> [false, env, xSM <+> eSM <+ STA]
            esac
        esac

    | Seq (s1, s2)      ->
        case genLabel (env) of
          [s2Lab, env] ->
            case compile (s2Lab, env, s1) of
              [s2LabUsed, env, s1SM] ->
                case compile (lab, env, s2) of
                  [labUsed, env, s2SM] -> [labUsed, env, s1SM <+> label (s2Lab, s2LabUsed) <+> s2SM]
                esac
            esac
        esac

    | Ignore (e)        ->
        case compile (lab, env, e) of
          [lu, env, instsSM] -> [lu, env, instsSM <+ DROP]
        esac

    | While (e, s)      ->
        case genLabels (env, 2) of
          [le, ls, env] ->
            case compile (lab, env, e) of
              [_, env, eSM] ->
                case compile (le, env, s) of
                  [_, env, sCode] ->
                    [false, env,
                      singletonBuffer (JMP (le)) <+
                      LABEL (ls)                 <+>
                      sCode                      <+
                      LABEL (le)                 <+>
                      eSM                        <+
                      CJMP ("nz", ls)]
                esac
            esac
        esac

    | If (e, s1, s2)    ->
        case genLabels (env, 2) of
          [lout, ls2, env] ->
            case compile (lab, env, e) of
              [_, env, eSM] ->
                case compile (lab, env, s1) of
                  [labUsed1, env, s1Code] ->
                    case compile (lab, env, s2) of
                      [labUsed2, env, s2Code] ->
                        [true, env,
                          eSM             <+
                          CJMP ("z", ls2) <+>
                          s1Code          <+
                          JMP (lout)      <+
                          LABEL (ls2)     <+>
                          s2Code          <+
                          LABEL (lout)]
                    esac
                esac
            esac
        esac

    | DoWhile (s, e)    ->
        case genLabels (env, 2) of
          [ll, le, env] ->
            case compile (lab, env, e) of
              [_, env, eSM] ->
                case compile (le, env, s) of
                  [labUsed, env, sCode] ->
                    [false, env,
                      singletonBuffer (LABEL (ll)) <+>
                      sCode                        <+>
                      eSM                          <+
                      CJMP ("nz", ll)]
                esac
            esac
        esac

    | Scope (defs, e)   ->
        case foldl (fun (efg, def) {registerDef (efg, def)}, [beginScope (env), {}, {}], defs) of
          [env, funs, globals] ->
            case compile (lab, foldl (fun (env, [fLabel, args, body]) {rememberFun (env, fLabel, args, body)}, env, funs), e) of
              [flag, env, code] ->
                  [flag, endScope (env), listBuffer (globals) <+> code]
            esac
        esac

    | Call (f, {})      ->
        case lookupFun (env, f) of
          Fun (fLabel, nargs) -> [false, env, singletonBuffer (CALL (fLabel, nargs))]
        esac

    | Call (f, args)    ->
        case genLabel (env) of
          [callLabel, env] ->
            case compileMany (callLabel, env, args) of
              [callUsed, env, argsSM] ->
                case lookupFun (env, f) of
                  Fun (fLabel, nargs) -> [false, env, argsSM <+> label (callLabel, callUsed) <+ CALL (fLabel, nargs)]
                esac
            esac
        esac

    | String (s)        -> [false, env, singletonBuffer (STRING (s))]

    | Array (a)         ->
        case compileMany (lab, env, a) of
          [_, env, aSM] -> [false, env, aSM <+ ARRAY (size (a))]
        esac

    | Sexp (s, {})      -> [false, env, singletonBuffer (SEXP (s, 0))]

    | Sexp (s, args)    ->
        case compileMany (lab, env, args) of
          [_, env, argsSM] -> [false, env, argsSM <+ SEXP (s, size (args))]
        esac

    | Elem (e, i)       ->
        case compileMany (lab, env, {e, i}) of
          [_, env, eiSM] -> [false, env, eiSM <+ ELEM]
        esac

    | ElemRef (e, i)    ->
        case compileMany (lab, env, {e, i}) of
          [_, env, eiSM] -> [false, env, eiSM]
        esac

    esac
  }

fun compileFunctions (env, main) {
  fun iterate (env, acc, funList) {
    case funList of
      {} ->
        case getFuns (env) of
          [{}, env] -> acc
        | [fs, env] -> iterate (env, acc, fs)
        esac
    | Fun (f, args, body, state) : fs ->
        case genLabel (addArgs (beginFun (env, state), args)) of
          [endLab, env] ->
            case compile (endLab, env, body) of
              [endUsed, env, code] ->
                iterate (env,
                          acc                                     <+
                          LABEL (f)                               <+
                          BEGIN (f, size (args), getLocals (env)) <+>
                          code                                    <+>
                          label (endLab, endUsed)                 <+
                          END,
                        fs)
            esac
        esac
    esac
  }

  iterate (env, main, {})
}

  case initCompEnv ().beginScope.addFun ("read"  , "$read"  , 0)
                                .addFun ("write" , "$write" , 1)
                                .addFun ("length", "$length", 1).genLabel of
    [endLab, env] ->
       case compile (endLab, env, expr) of
         [endUsed, env, code] ->
              compileFunctions (env,
                                singletonBuffer (LABEL ("main"))   <+
                                BEGIN ("main", 0, getLocals (env)) <+>
                                code                               <+>
                                label (endLab, endUsed)            <+
                                END).fst
       esac
  esac
}
