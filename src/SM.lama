-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun evalSMRead ([stack, state, world], READ) {
  var readRes = readWorld (world);
  [readRes[0]:stack, state, readRes[1]]
}

fun evalSMWrite ([value:stack, state, world], WRITE) {
  [stack, state, writeWorld (value, world)]
}

fun evalSMBinop ([r:l:stack, state, world], BINOP (op)) {
  var result = 
  case op of
    "+"  -> l +  r
  | "-"  -> l -  r
  | "*"  -> l *  r
  | "/"  -> l /  r
  | "%"  -> l %  r
  | "<"  -> l <  r
  | ">"  -> l >  r
  | "==" -> l == r
  | "<=" -> l <= r
  | ">=" -> l >= r
  | "!=" -> l != r
  | "&&" -> l && r
  | "!!" -> l !! r
  | _    -> failure ("Invalid binary operation!")
  esac;
  [result:stack, state, world]
}

fun evalSMLD ([stack, state, world], LD (x)) {
  [state (x):stack, state, world]
}

fun evalSMST ([value:stack, state, world], ST (variable)) {
  [stack, state <- [variable, value], world]
}

fun evalSMConst ([stack, state, world], CONST (value)) {
  [value:stack, state, world]
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  foldl (fun (conf, i) {
    case i of
      READ      -> evalSMRead   (conf, i)
    | WRITE     -> evalSMWrite  (conf, i)
    | BINOP (s) -> evalSMBinop  (conf, i)
    | LD    (x) -> evalSMLD     (conf, i)
    | ST    (x) -> evalSMST     (conf, i)
    | CONST (n) -> evalSMConst  (conf, i)
    | _         -> failure      ("Invalid instruction!")
    esac
  }, c, insns)
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions

fun compileExprVar (Var (variable)) {
  singletonBuffer(LD (variable))
}

fun compileExprConst (Const (n)) {
  singletonBuffer (CONST (n))
}

fun compileExprBinop(Binop (op, expr1, expr2)) {
  compileExpr(expr1) <+> compileExpr(expr2) <+ BINOP (op)
}

fun compileExpr (expr) {
  case expr of
    Var   (_)       -> compileExprVar   (expr)
  | Const (_)       -> compileExprConst (expr)
  | Binop (_, _, _) -> compileExprBinop (expr)
  | _               -> failure          ("Invalid compiling expression type!")
  esac
}

fun compileStmtAssn (Assn (variable, expr)) {
  compileExpr (expr) <+ ST (variable)
}

fun compileStmtSeq (Seq (stmt1, stmt2)) {
  compileSMBuffer(stmt1) <+> compileSMBuffer(stmt2)
}

fun compileStmtRead (Read (variable)) {
  listBuffer({READ, ST (variable)})
}

fun compileStmtWrite (Write (expr)) {
  compileExpr (expr) <+ WRITE
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  getBuffer (compileSMBuffer (stmt))
}

public fun compileSMBuffer (stmt) {
  case stmt of
    Assn  (_, _) -> compileStmtAssn  (stmt)
  | Seq   (_, _) -> compileStmtSeq   (stmt)
  | Skip         -> emptyBuffer      ()
  | Read  (_)    -> compileStmtRead  (stmt)
  | Write (_)    -> compileStmtWrite (stmt)
  | _            -> failure          ("Invalid compiling statement!")
  esac
}
