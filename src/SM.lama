-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;
import Expr;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c, insns) {
  foldl (evalOne, c, insns)
}

fun evalOne ([vstack, state, world], ins) {
  case ins of
    READ      -> case readWorld (world) of [value, newWorld] -> [value : vstack, state, newWorld] esac
  | WRITE     -> case vstack of head:tail -> [tail, state, writeWorld (head, world)] esac
  | BINOP (s) -> case vstack of r:l:tail -> [opValue (s) (l, r) : tail, state, world] esac
  | LD    (x) -> [state (x) : vstack, state, world]
  | ST    (x) -> case vstack of head:tail -> [tail, state <- [x, head], world] esac
  | CONST (n) -> [n : vstack, state, world]
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of 
    Var   (name)      -> {LD (name)}
  | Const (value)     -> {CONST (value)}
  | Binop (op, l, r)  -> compileExpr (l) +++ compileExpr (r) +++ {BINOP (op)}
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  case stmt of 
    Assn  (name, expr)   -> compileExpr (expr) +++ {ST (name)}
  | Seq   (stmt1, stmt2) -> compileSM (stmt1) +++ compileSM (stmt2)
  | Skip                 -> {}
  | Read  (name)         -> {READ, ST (name)}
  | Write (expr)         -> compileExpr (expr) +++ {WRITE}
  esac
}
