-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun evalSingle(c@[stack, state, world], ins) {
  case ins of 
    READ -> (
      var p = readWorld(world);
      [p.fst : stack, state, p.snd]
    )
  | WRITE -> (
    case stack of 
      hd : tail -> [tail, state, writeWorld(hd, world)]
    esac
  )
  | BINOP (oper) -> (
    case stack of 
      r : l : tail -> [evalExpr(state, Binop(oper, Const(l), Const(r))) : tail, state, world]
    esac
  )
  | LD (x) ->  [state(x) : stack, state, world]
  | ST (x) -> (
    case stack of 
      hd : tail -> [tail, state <- [x, hd], world]
    esac
  )
  | CONST(n) -> [n : stack, state, world]
  | LABEL(s) -> (
    c
  )
  esac
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, world], insns) {
  case insns of 
    hd : tail -> (
      case hd of 
        JMP(l) ->
          eval (env, c, fromLabel(env, l))
      | CJMP ("z", l) -> 
        case stack of value : stack_tail ->
          eval(env, [stack_tail, state, world], if value == 0 then fromLabel(env, l) else tail fi)
        esac
      | CJMP ("nz", l) -> 
        case stack of value : stack_tail -> 
          eval(env, [stack_tail, state, world], if value == 1 then fromLabel(env, l) else tail fi)
        esac
      | oth -> eval(env, evalSingle(c, hd), tail)
      esac
    )
    | {} -> c
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions

fun compileExpr (expr) {
  case expr of 
    Var(x) -> (
      singletonBuffer(LD (x))
    )
  | Const(n) -> (
      singletonBuffer(CONST (n))
    )
  | Binop(s, l, r) -> (
      compileExpr(l) <+> compileExpr(r) <+> singletonBuffer(BINOP(s))
    )

  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.

public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  

  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of 
                    [labUsed, env, s2Code] -> ([labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code])
                  esac
             esac
        esac
    | If(e, s1, s2) ->
      case env.genLabels(2) of 
        [s1_end_label, s2_begin_label, env] ->
          case compile(s1_end_label, env, s1) of 
            [s1_end_used, env, s1_code] -> 
              case compile(lab, env, s2) of
                [lab_used, env, s2_code] -> (
                  var t = compileExpr(e) <+
                    CJMP("z", s2_begin_label) <+>
                    s1_code <+> 
                    label (s1_end_label, s1_end_used) <+
                    JMP(lab) <+>
                    label(s2_begin_label, true) <+>
                    s2_code <+
                    JMP(lab);
                  [true, env, 
                    t
                  ]

                )
                
              esac
          esac
      esac
    | While(e, s) ->
      case env.genLabels(2) of 
        [l_e, l_s, env] -> 
        case compile(l_e, env, s) of
          [l_e_used, env, s_code] ->
          [true, env,
            singletonBuffer(JMP(l_e)) <+>
            label (l_s, true) <+>
            s_code <+>
            label(l_e, true) <+>
            compileExpr(e) <+
            CJMP("nz", l_s) <+
            JMP(lab)
          ]
        esac
      esac
    | DoWhile(s, e) ->
      case env.genLabels(2) of 
        [s_begin, s_end, env] ->
        case compile(s_end, env, s) of 
          [s_end_used, env, s_code] ->
          [true, env, 
            label(s_begin, true) <+>
            s_code <+>
            label(s_end, s_end_used) <+>
            compileExpr(e) <+
            CJMP("nz", s_begin) <+
            JMP(lab)
          ]
        esac

      esac
    | _              -> failure ("compileSM not implemented\n")
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> ( 
            var t = getBuffer $  code <+> label (endLab, endLabUsed);
            t
          )
       esac
  esac
}
