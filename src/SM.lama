-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }

    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }

  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});

  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac
  }

  -- Assigns a value to a location
  fun assign (state@[args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac;
    state
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  fun createLocal(s, na, nl) {
    case take(s, na) of
      [tail, args] -> [tail, [listArray(args), initArray (nl, fun (_) {0})]]
    esac
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (c@[stack, call_stack, state, world], prog) {
    case prog of
    {} -> c
    | i:prog ->
    -- fprintf(stderr, "%s\n", showSMInsn(i));
    -- fprintf(stderr, "state: %s\n", state.string);
    -- fprintf(stderr, "stack: %s\n", stack.string);
      case i of
        READ      ->  case readWorld (world) of
                        [z, tail] -> eval([z:stack, call_stack, state, tail], prog)
                      esac
      | WRITE     ->  case stack of
                        h : tail -> eval([tail, call_stack, state, writeWorld (h, world)], prog)
                      esac
      | BINOP (s) ->  case stack of
                        y : x : tail -> eval([evalOp (s, x, y) : tail, call_stack, state, world], prog)
                      esac
      | LD    (loc) ->
      eval([lookup (state, loc) : stack, call_stack, state, world], prog)
      | CONST (n) ->  eval([n : stack, call_stack, state, world], prog)
      | LABEL (_) ->  eval(c, prog)
      | JMP (lab) ->  eval(c, fromLabel(env, lab))
      | CJMP ("nz", lab) -> case stack of
                              0 : tail -> eval([tail, call_stack, state, world], prog)
                            | n : tail -> eval([tail, call_stack, state, world], fromLabel(env, lab))
                            esac
      | CJMP ("z", lab) -> case stack of
                              0 : tail -> eval([tail, call_stack, state, world], fromLabel(env, lab))
                            | n : tail -> eval([tail, call_stack, state, world], prog)
                            esac
      | LDA (loc) ->  eval([Ref (loc) : stack, call_stack, state, world], prog)
      | STA       ->  case stack of
                          v : Ref (x) : tail -> eval([v : tail, call_stack, assign(state, x, v), world], prog)
                        | v : i :  a  : tail   -> eval([(unwrapSexp(a)[i] := v) : tail, call_stack, state, world], prog)
                      esac
      | DROP      ->  eval([tl(stack), call_stack, state, world], prog)
      | END       ->  case call_stack of
                        {} -> c
                      | [sigma_l, q] : s_c -> eval([stack, s_c, sigma_l, world], q)
                      esac
      | BEGIN (_, na, nl) ->
        case createLocal(stack, na, nl) of
          [s_stroke, sigma_l] -> eval([s_stroke, call_stack, sigma_l, world], prog)
        esac
      | CALL (f, _) -> eval([stack, [state, prog] : call_stack, state, world], fromLabel(env, f))
      | GLOBAL (name) -> eval([stack, call_stack, assign(state, Glb (name), 0), world], prog)
      | STRING (s) -> eval([s: stack, call_stack, state, world], prog)
      | ARRAY (n) -> case take(stack, n) of
                       [rem, top] -> eval([listArray(top) : rem, call_stack, state, world], prog)
                     esac
      | ELEM -> case stack of
                  i : a : tail -> eval([unwrapSexp(a)[i] : tail, call_stack, state, world], prog)
                esac
      | BUILTIN (f, n) ->  case take(stack, n) of [rem, top] ->
                              case evalBuiltin(f, top, world) of [value, world] ->
                                eval([value : rem, call_stack, state, world], prog)
                              esac
                           esac
      | SEXP(s, n) -> case take(stack, n) of
                       [rem, top] -> eval([Sexp(s, listArray(top)) : rem, call_stack, state, world], prog)
                      esac
      esac
    esac
  }

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}



-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }

  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }

  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.


-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun compileFunction(env, Fun (fname, args, body, state)) {
    case genLabel (addArgs (beginFun (env, state), args)) of
      [fLab, env] ->
        case compile (fLab, env, body) of
          [fLabUsed, env, bCode] -> [env,
            singletonBuffer (LABEL (fname)) <+
            BEGIN (fname, size(args), getLocals(env)) <+>
            bCode <+>
            label (fLab, fLabUsed) <+
            END]
        esac
    esac
  }

  fun compileFunctions(env, fs) {
    case fs of
      {}     -> emptyBuffer()
    | f : fs -> case compileFunction(env, f) of
                  [env, fCode] -> fCode <+> compileFunctions(env, fs)
                esac
    esac
  }

  fun compileList(env, stmts) {
    foldl(fun ([code, env], stmt) {
        case genLabel(env) of
          [argLab, env] ->
            case compile(argLab, env, stmt) of
              [argLabUsed, env, argCode] -> [ code <+>
                                              argCode <+>
                                              label(argLab, argLabUsed), env]
            esac
        esac
      }, [emptyBuffer(), env], stmts)
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer
  fun compile (lab, env, stmt) {
    -- fprintf(stderr, "%s\n", stmt.string);
    case stmt of
      Skip              -> [false, env, emptyBuffer ()]
    | Var    (x)        -> [false, env, singletonBuffer (LD (lookupVal(env, x)))]
    | Val    (x)        -> [false, env, singletonBuffer (LD (lookupVal(env, x)))]
    | Ref    (x)        -> [false, env, singletonBuffer (LDA (lookupVar(env, x)))]
    | Const  (n)        -> [false, env, singletonBuffer (CONST (n))]
    | Assn  (x, e)      ->
      case compileList(env, {x, e}) of
        [code, env] -> [false, env, code <+ STA]
      esac
    | Seq (s1, s2)      ->
      case compileList(env, {s1, s2}) of
        [code, env] -> [false, env, code]
      esac
    | If (e, s1, s2)    ->
        case genLabels(env, 3) of
          [condLab, eLab, endTrueLab, env] ->
            case compile (endTrueLab, env, s1) of
              [trueUsed, env, s1Code] ->
                case compile (lab, env, s2) of
                  [_, env, s2Code] ->
                    case compile (condLab, env, e) of
                      [condLabUsed, env, condCode] -> [true, env,
                                                    condCode <+>
                                                    label(condLab, condLabUsed) <+
                                                    CJMP("z", eLab) <+>
                                                    s1Code <+>
                                                    label(endTrueLab, trueUsed) <+
                                                    JMP(lab) <+
                                                    LABEL(eLab) <+>
                                                    s2Code]
                    esac
                esac
            esac
        esac
    | While (e, s)      ->
        case genLabels(env, 3) of
          [condLab, beginLab, endLab, env] ->
            case compile (endLab, env, s) of
              [genEnd, env, sCode] ->
                case compile(condLab, env, e) of
                  [condLabUsed, env, condCode] -> [true, env,
                                                  singletonBuffer(LABEL(beginLab)) <+>
                                                  condCode <+>
                                                  label(condLab, condLabUsed) <+
                                                  CJMP("z", lab) <+>
                                                  sCode <+>
                                                  label(endLab, genEnd) <+
                                                  JMP(beginLab)]
                esac
            esac
        esac
    | DoWhile(s, e)     ->
        case genLabels(env, 3) of
          [condLab, beginLab, endLab, env] ->
            case compile (endLab, env, s) of
              [genEnd, env, sCode] ->
              case compile (condLab, env, e) of
                [condLabUsed, env, condCode] -> [false, env,
                                                   singletonBuffer(LABEL(beginLab)) <+>
                                                   sCode <+>
                                                   label(endLab, genEnd) <+>
                                                   condCode <+>
                                                   label(condLab, condLabUsed) <+
                                                   CJMP("nz", beginLab)]
              esac
            esac
        esac
    | Binop(op, e1, e2) ->
      case compileList(env, {e1, e2}) of
        [code, env] -> [false, env, code <+ BINOP(op)]
      esac
    | Ignore (e) ->
      case compileList(env, {e}) of
        [code, env] -> [false, env, code <+ DROP]
      esac
    | Scope (defs, expr) ->
      case foldl(fun ([code, env], def) {
          case def of
              Fun (name, args, body) ->
                case genFunLabel(env, name) of
                  [label, env] -> [code, rememberFun(env, label, args, body)]
                esac
            | _ -> [code, env]
          esac
        }, foldl(fun ([code, env], def) {
          case def of
              Fun (name, args, body) ->
                case genFunLabel(env, name) of
                  [label, env] -> [code, addFun(env, name, label, size(args))]
                esac
            | Var (name)       -> [if isGlobal(env) then code <+ GLOBAL (name) else code fi, addVar(env, name)]
          esac
        }, [emptyBuffer(), beginScope(env)], defs), defs) of
        [gCode, env] ->
          case compile(lab, env, expr) of
            [labUsed, env, eCode] -> [labUsed, endScope(env), gCode <+> eCode]
          esac
      esac
    | Call (f, arg_vals) ->
      case compileList(env, arg_vals) of
        [code, env] ->
          case lookupFun(env, f) of
            Fun(fLabel, nargs) -> [false, env, code <+ CALL(fLabel, nargs)]
          esac
      esac
    | String (s) -> [false, env, singletonBuffer(STRING(s))]
    | Array (vals) ->
      case compileList(env, vals) of
        [code, env] -> [false, env, code <+ ARRAY(size(vals))]
      esac
    | Sexp (name, vals) ->
      case compileList(env, vals) of
        [code, env] -> [false, env, code <+ SEXP(name, size(vals))]
      esac
    | Elem (arr, i) ->
      case compileList(env, {arr, i}) of
        [code, env] -> [false, env, code <+ ELEM]
      esac
    | ElemRef(arr, i) ->
      case compileList(env, {arr, i}) of
        [code, env] -> [false, env, code]
      esac
    | Builtin(name, args) ->
      case compileList(env, args) of
        [code, env] -> [false, env, code <+ BUILTIN(name, size(args))]
      esac
    esac
  }

  case initCompEnv ().beginScope.addFun ("read"  , "$read"  , 0)
                                .addFun ("write" , "$write" , 1)
                                .addFun ("length", "$length", 1).genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, env, code] ->
         case getFuns(env) of
          [fs, env] -> getBuffer $ singletonBuffer(LABEL("main")) <+
                                   BEGIN("main", 0, getLocals(env)) <+>
                                   code <+>
                                   label (endLab, endLabUsed) <+
                                   END <+>
                                   compileFunctions(env, fs)
         esac
       esac
  esac
}
