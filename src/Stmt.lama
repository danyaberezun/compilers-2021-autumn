-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval ([st, world], stmt) {
  case stmt of 
    Skip -> [st, world]
  | Assn (x, expr) -> [st <- [x, evalExpr(st, expr)], world]
  | Seq (left_stmt, right_stmt) -> eval(eval([st, world], left_stmt), right_stmt)
  | Write (expr) -> [st, writeWorld(evalExpr(st, expr), world)]
  | Read (x) -> case readWorld(world) of 
      [read_value, new_world] -> [st <- [x, read_value], new_world] 
    esac  
  | If (cond, thenStmt, elseStmt) -> if evalExpr(st, cond) != 0 then eval([st, world], thenStmt) else eval([st, world], elseStmt) fi 
  | While (cond, body) -> if evalExpr(st, cond) != 0 then case eval([st, world], body) of 
      [st, world] -> eval([st, world], While (cond, body))
    esac
    else [st, world] fi 
  | DoWhile (body, cond) -> case eval([st, world], body) of 
      [st, world] -> eval([st, world], While (cond, body))
    esac 
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}