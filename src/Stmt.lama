-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[st, w], stmt) {
  case stmt of
    Assn (x, e) -> [st <- [x, evalExpr(st, e)], w]
    | Seq (s1, s2) -> case eval(c, s1) of
                        [st, w] -> eval([st, w], s2)
                      esac 
    | Skip -> c
    | Read (x) -> case readWorld(w) of 
                    [n, w] -> [st <- [x, n], w]
                  esac
    | Write (e) -> [st, writeWorld(evalExpr(st, e), w)]
    | If (e, s1, s2) -> if evalExpr(st, e)
                        then eval(c, s1)
                        else eval(c, s2)
                        fi
    | While (e, s1) -> if evalExpr(st, e)
                       then eval(c, Seq(s1, stmt))
                       else c
                       fi
    | DoWhile (s1, e) -> case eval(c, s1) of 
                          c@[st, w] -> 
                            if evalExpr(st, e)
                            then eval(c, stmt)
                            else c
                            fi
                        esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  -- failure(stmt.string);
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
