-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval(c@[state, world], statement) {
  case statement of
    Assn(name, expr) -> [state <- [name, evalExpr(state, expr)], world]
    | Seq(left, right) -> eval(eval(c, left), right)
    | Skip -> [state, world]
    | Read(name) -> (
      var t = readWorld(world);
      [state <- [name, t.fst], t.snd])
    | Write(expr) -> [state, writeWorld(evalExpr(state, expr), world)]
    | If (condExpr, ifTrue, ifFalse) -> eval(c, if evalExpr(state, condExpr) then ifTrue else ifFalse fi)
    | While(condExpr, statement) ->
      var cur = c;
      while evalExpr(cur[0], condExpr) do
        cur := eval(cur, statement)
      od;
      cur
    | DoWhile(statement, condExpr) ->
      var cur = c;
      do
        cur := eval(cur, statement)
      while evalExpr(cur[0], condExpr) od;
      cur
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt(input, statement) {
  eval([emptyState, createWorld(input)], statement).snd.getOutput
}
