-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c, stmt) {
  case stmt of
    Assn(name, expr) ->
      case c of
        [st, w] -> [st <- [name, evalExpr(st, expr)], w]
      esac                                                |
    Seq(l, r)   -> eval(eval(c, l), r)                    |
    Skip        -> c                                      |
    Read (name) ->
      case c of
        [st, w] ->
          case readWorld(w) of
            [rSt, rw] -> [st <- [name, rSt], rw]
          esac
      esac                                                |
    Write (expr) ->
      case c of
        [st, w] -> [st, writeWorld(evalExpr(st, expr), w)]
      esac                                                |
    If (ifExpression, thenStmt, elseStmt) ->
      case c of
        [st, w] ->
          case evalExpr(st, ifExpression) of
            0 -> eval(c, elseStmt) |
            _ -> eval(c, thenStmt)
          esac
      esac                                                |
    While (whileExpression, whileStmt) ->
      case c of
        [st, w] ->
          case evalExpr(st, whileExpression) of
            0 -> c |
            _ -> eval(eval(c, whileStmt), stmt)
          esac
      esac                                                |
    DoWhile (doWhileStmt, doWhileExpression) ->
      case eval(c, doWhileStmt) of
        newC ->
          case newC of
            0 -> newC                                 |
            _ -> eval(
                eval(newC, doWhileStmt),
                While(doWhileExpression, doWhileStmt)
              )
          esac
      esac                                                |
    _ -> failure("Eval is not implemented for statement.")
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
