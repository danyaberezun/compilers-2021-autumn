-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (xs@[st, world], stmt) {
  case stmt of 
    Assn (x, expr) -> [st <- [x, evalExpr(st, expr)], world]
    | Seq (stmt1, stmt2) -> eval(eval(xs, stmt1), stmt2)
    | Skip -> xs
    | Read (x) -> 
      case readWorld(world) of 
        [v, nWorld] -> [st <- [x, v], nWorld]
      esac
    | Write (e) -> [st, writeWorld(evalExpr(st, e), world)]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
