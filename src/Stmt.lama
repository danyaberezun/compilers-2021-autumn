-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (config@[state, world], stmt) {
  case stmt of
    Assn (varName, expr) -> [state <- [varName, evalExpr (state, expr)], world]
  | Seq (first, second)  -> eval (eval (config, first), second)
  | Skip                 -> config
  | Read (varName)       -> case readWorld (world) of [value, newWorld] -> [state <- [varName, value], newWorld] esac
  | Write (e)            -> [state, writeWorld(evalExpr (state, e), world)]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
