-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (conf@[state, world], stmt) {
  case stmt of
    Skip -> conf
  | Assn (name, expr) -> [state <- [name, evalExpr (state, expr)], world]
  | Seq (stmt1, stmt2) -> eval (eval (conf, stmt1), stmt2)
  | Read (name) -> case readWorld (world) of [x, world] -> [state <- [name, x], world] esac
  | Write (expr) -> [state, writeWorld (evalExpr (state, expr), world)]
  | If (cond, ifThen, ifElse) ->
      if evalExpr (state, cond)
      then eval (conf, ifThen)
      else eval (conf, ifElse)
      fi
  | lp@While (cond, block) ->
      if evalExpr (state, cond)
      then eval (eval (conf, block), lp)
      else conf
      fi
  | DoWhile (cond, block) ->
      eval (eval (conf, block), While (cond, block))
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
