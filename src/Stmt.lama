-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (c, stmt) {
  -- c = [ state, world ]
  case stmt of
    Assn (x, expr) -> [c.fst <- [x, evalExpr(c.fst, expr)], c.snd]
  | Seq  (stmt1, stmt2) -> eval(eval(c, stmt1), stmt2)
  | Skip -> c
  | Read (x) ->
    case readWorld(c.snd) of
      [v, w] -> [c.fst <- [x, v], w]
    esac
  | Write (expr) -> [c.fst, writeWorld(evalExpr(c.fst, expr), c.snd)]
  esac

  -- failure ("Stmt eval not implemented\n")
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
