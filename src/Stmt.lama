-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (c, stmt) {
  var state = c.fst;
  var world = c.snd;

  case stmt of
  	Skip -> skip
  |	Assn (string, expr) -> state := state <- [string, evalExpr(state, expr)]
  |	Seq (stm1, stm2) -> 
  		case eval(c, stm1) of [s, w] -> state := s; world := w esac;
  		case eval([state, world], stm2) of [s, w] -> state := s; world := w esac
  |	Read (x) -> case readWorld(world) of [n, w] -> state := state <- [x, n]; world := w esac
  |	Write (expr) -> world := writeWorld(evalExpr(state, expr), world)
  esac;
  [state, world]
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
